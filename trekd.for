C=================================================
C
C  TREK7 MODULE D
C CONVERTED TO PC BY: DAN GAHLINGER
C ENTIRE MODULE TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C  SHIP INITIALIZATION AND TERMINATION
C
C HADES   TOPHET  EREBUS  CHARON  STYX  BELIAL
C HARLIE  FIZBIN  TIMEX   MENDEZ  KZIN
C
C=================================================


C DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C EDITS FROM ULLI THE VAXORCIST MARKED "CV" OR "!V"
C
C - HADES -
C

CV ==========================
CV INPUT TRANSFER PARAMETERS
CV ==========================
CV I      - INDEX OF VESSEL TO DESTROY

CV C-O VESSEL DESTRUCTION (INCLUDING DOOMSDAY AND COLLATERAL DESTRUCTION)

      SUBROUTINE HADES
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NDSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /I/IONK(8),ISPOK(8),ION(4),ISPOT(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /R/IBPSC(4),IBPSB(8),IBPSS(2),IBPSE(25)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)

CV SET N TO INDEX OF VESSEL TO DESTROY
      N=I

CV SET J TO FOREIGN OWNER FLAG OF VESSEL TO DESTROY
      J=IBPSB(N)

CV IF FOREIGN OWNER: 54
      IF(J.NE.0)GO TO 54

CV DECREMENT NUMBER OF C-O VESSELS IN GAME
      I3=I3-1

CV GO TO "DESTRUCTION OF OBJECT INDEX N"
      GO TO 763

CV DECREMENT TOTAL NUMBER OF PARTY (J) PARTICIPANTS
54    II(J)=II(J)-1

CV RESET FOREIGN OWNER FLAG
      IBPSB(N)=0

CV DESTRUCTION OF OBJECT INDEX N
763   CALL TOPHET(N)

CV =============================================
CV DOOMSDAY DESTRUCTION COLLATERAL DESTRUCTIONS
CV =============================================

CV IF OBJECT NOT DOOMSDAY: RETURN
      IF(N.NE.7)RETURN

CV ELSE: OBJECT IS DOOMSDAY: DESTRUCTION AROUND IT (UP TO 7 UNITS AWAY)

CV SET MINIMUM DESTRCTION ROW
766   IV=I7-7

CV SET MINIMUM DESTRCTION COL
      IVV=I8-7

CV SET MAXIMUM DESTRCTION ROW
      IV1=I7+7

CV SET MAXIMUM DESTRCTION COL
      IZA=I8+7

CV LIMIT MINIMUM DESTRCTION ROW
      IF(IV.LE.0)IV=1

CV LIMIT MINIMUM DESTRCTION COL
      IF(IVV.LE.0)IVV=1

CV LIMIT MAXIMUM DESTRCTION ROW
      IF(IV1.GT.60)IV1=60

CV LIMIT MAXIMUM DESTRCTION COL
      IF(IZA.GT.60)IZA=60

CV LOOPS OVER ALL ENDANGERED MAP POSITIONS
      DO 780 N=IV,IV1
      DO 780 J=IVV,IZA

CV -----------
CV UPDATE MAP
CV -----------

CV IF OBJECT INSIDE DOOMSDAY DESTRUCTION AREA (RADIUS 7):
CV DELETE OBJECT FROM MAP
      IF((I8-J)**2+(I7-N)**2.LE.49)MAP(N,J)=IBLK

CV NEXT MAP POSITION
780   CONTINUE

CV ====================================
CV C-O DESTRUCTION BY DOOMSDAY MACHINE
CV ====================================

CV LOOP OVER C-O SHIPS N FOR 1 TO 8
      DO 769 N=1,8

CV IF C-O SHIP NOT IN GAME: NEXT C-O SHIP
      IF(ICHOS(N).EQ.0)GO TO 769

CV IF "C-O SHIP" = DOOMSDAY: NEXT C-O SHIP
      IF(N.EQ.7)GO TO 769

CV IF C-O SHIP OUTSIDE DOOMSDAY DESTRUCTION AREA:
CV SKIP C-O SHIP DESTRUCTION
      IF((IKLNC(N)-I8)**2+(IKLNR(N)-I7)**2.GT.49)GO TO 769

CV RESET ION STORM FLAG OF C-O SHIP
      ISPOK(N)=0

CV DESTRUCTION OF OBJECT INDEX N
      CALL TOPHET(N)

CV NEXT C-O SHIP
769   CONTINUE

CV =======================================
CV EAGLES DESTRUCTION BY DOOMSDAY MACHINE
CV =======================================

CV IF NO EAGLES OUT, SKIP EAGLES HANDLING
      IF(NUMOUT.EQ.0)GO TO 7690

CV LOOP OVER EAGLES N FOR 1 TO LAUNCH
      DO 7691 N=1,LAUNCH

CV IF EAGLE(N) NOT IN GAME: NEXT EAGLE
      IF(IGLER(N).EQ.0)GO TO 7691

CV IF EAGLE OUTSIDE DOOMSDAY DESTRUCTION AREA: SKIP EAGLE DESTRUCTION
      IF((IGLER(N)-I7)**2+(IGLEC(N)-I8)**2.GT.49)GO TO 7691

CV LOOP OVER M-O SHIPS KOENIG FOR 1 TO 4
      DO 540 KOENIG=1,4

CV IF M-O SHIP NOT IN GAME: SKIP WRITE MESSAGE & NEXT MO-SHIP
      IF(ICHOE(KOENIG).EQ.0)GO TO 540

CV CALCULATE FORTRAN UNIT NUMBER OF M-O SHIP
      J=KOENIG+4

CV WRITE M-O SHIP MESSAGE "EAGLE <NUMBER> DESTROYED BY EXPLOSION"
      WRITE(J,7692)N
7692  FORMAT(' EAGLE',I3,' DESTROYED BY EXPLOSION')

CV NEXT MO-SHIP
540   CONTINUE

CV EAGLES HANDLING ON DESTRUCTION
      CALL BOOM(N)

CV NEXT EAGLE
7691  CONTINUE

CV ====================================
CV M-O DESTRUCTION BY DOOMSDAY MACHINE
CV ====================================

CV LOOP OVER M-O SHIPS KOENIG FOR 1 TO 4
CV (WHICH SHIPS ARE DESTRUCTED)
7690  DO 55 KOENIG=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(KOENIG).EQ.0)GO TO 55

CV IF M-O SHIP OUTSIDE DOOMSDAY DESTRUCTION AREA: SKIP M-O SHIP DESTRUCTION
      IF((IENTC(KOENIG)-I8)**2+(IENTR(KOENIG)-I7)**2.GT.49)GO TO 55

CV LOOP OVER M-O SHIPS J FOR 1 TO 4
CV (WHICH SHIPS SHALL GET THE MESSAGE)
      DO 56 J=1,4

CV IF M-O SHIP NOT IN GAME: SKIP WRITE MESSAGE
CV "THE <M-O SHIP NAME> HAS BEEN ENGULFED BY THE EXPLOSION ..."
      IF(ICHOE(J).EQ.0)GO TO 56

CV CALCULATE FORTRAN UNIT NUMBER OF M-O SHIP
      IV=J+4

CV WRITE MESSAGE
CV "THE <M-O SHIP NAME> HAS BEEN ENGULFED BY THE EXPLOSION ..."
      WRITE(IV,772)(INAME(IQ0,KOENIG),IQ0=1,3)
772   FORMAT(' THE ',3A4,' HAS BEEN ENGULFED BY THE EXPLOSION'/' FROM
     * THE DOOMSDAY MACHINE')

CV NEXT M-O SHIP
56    CONTINUE

CV SET M-O SHIP DESTRUCTION FLAG
      DFLCT(KOENIG)=-10.

CV NEXT M-O SHIP
55    CONTINUE

CV =========================================
CV STARBASE DESTRUCTION BY DOOMSDAY MACHINE
CV =========================================

CV LOOP OVER STARBASES N FOR 1 TO 2
      DO 58 N=1,2

CV IF STARBASE NOT IN GAME: SKIP STARBASE DESTRUCTION
      IF(ICHOB(N).EQ.0)GO TO 58

CV IF STARBASE OUTSIDE DOOMSDAY DESTRUCTION AREA: SKIP STARBASE DESTRUCTION
      IF((IBASC(N)-I8)**2+(IBASR(N)-I7)**2.GT.49)GO TO 58

CV SET STARBASE DESTRUCTION FLAG
      DFLCB(N)=-10.

CV LOOP OVER M-O SHIPS J FOR 1 TO 4
      DO 580 J=1,4

CV IF M-O SHIP NOT IN GAME: SKIP MESSAGE "... EXPLOSION FROM DOOMSDAY"
      IF(ICHOE(J).EQ.0)GO TO 580

CV CALCULATE M-O SHIP FORTRAN UNIT NUMBER
      I7=J+4

CV WRITE MESSAGE "... EXPLOSION FROM DOOMSDAY"
      WRITE(I7,59)(ISIDE(IQ0,N),IQ0=1,3),IBASE(N)
59    FORMAT(1X,3A4,' STARBASE',I3,' ENGULFED BY EXPLOSION FROM DOOMSDAY
     * MACHINE')

CV NEXT M-O SHIP
580   CONTINUE

CV NEXT STARBASE
58    CONTINUE

CV ===============================================
CV DILITHIUM STAR DEGRADATION BY DOOMSDAY MACHINE
CV ===============================================

CV LOOP OVER DILITHIUM STARS N FOR 1 TO 5
      DO 57 N=1,5

CV IF DILITHIUM STAR NOT IN GAME: NEXT DILITHIUM STAR
      IF(LI2R(N).EQ.0)GO TO 57

CV IF DILITHIUM STAR OUTSIDE DOOMSDAY DESTRUCTION AREA:
CV NEXT DILITHIUM STAR
      IF((LI2R(N)-I7)**2+(LI2C(N)-I8)**2.GT.49)GO TO 57

CV ELSE: REMOVE DILITHIUM FROM STAR
      LI2R(N)=0
      LI2C(N)=0

CV NEXT DILITHIUM STAR
57    CONTINUE
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 ---
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C - TOPHET
C

CV DESTRUCTION OF C-O VESSEL "N"
CV "NX" IN COMMON BLOCK ON PURPOSE!!!

      SUBROUTINE TOPHET(N)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,NX,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /I/IONK(8),ISPOK(8),ION(4),ISPOT(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /S/IBPOB(8),IBPOE(25)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /V/IWEB(2),IWEBZ,INVIS(4)

CV REMOVE C-O SHIP(N) FROM GAME
      ICHOS(N)=0

CV IF OBJECT = DOOMSDAY: SKIP STARBASE ACTIONS
      IF(N.EQ.7)GO TO 52

CV IF OBJECT HAS NO BOARDERS: SKIP STARBASE ACTIONS
      IF(IBPOB(N).EQ.0)GO TO 52

CV SET STARBASE BOARDERS TO "0"
      IBPOB(N)=0

CV DECREMENT NUMER OF DOOMSDAY BOARDERS (?????)
      IBPOB(7)=IBPOB(7)-1

CV SET I7 TO ROW OF C-O SHIP(N)
52    I7=IKLNR(N)

CV SET I8 TO COL OF C-O SHIP(N)
      I8=IKLNC(N)

CV SET ROW/COL OF DELETED OBJEKT TO "0"
      IKLNR(N)=0
      IKLNC(N)=0

CV IF LAST PARTICIPANT IN GAME (ISTAT.EQ.2): SKIP DELETE OBJECT AT I7/I8
      IF(ISTAT.EQ.2)GO TO 165

CV DELETE OBJECT AT I7/I8
      MAP(I7,I8)=IBLK

CV IF ION STORM FLAG SET: RESET ION STORM ON I7/I8
      IF(ISPOK(N).EQ.1)MAP(I7,I8)=III

CV LOOP OVER ALL M-O SHIPS
165   DO 53 IV=1,4

CV IF SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(IV).EQ.0)GO TO 53

CV CALCULATE FORTRAN UNIT NUMBER J
      J=IV+4

CV WRITE MESSAGE TO M-O SHIPS IN GAME
CV "... AN EXPLOSION HAS PULVERIZED THE <C-O SHIP> ..."
      WRITE(J,102)(IENM2(MIN,N),MIN=1,4)
102   FORMAT(/' SENSORS REPORT THAT AN EXPLOSION HAS PULVERIZED THE '14A
     *4/'    INTO POWDERED DUST')

CV NEXT M-O SHIP
53    CONTINUE

CV LOOP OVER ALL WEBS
      DO 50 J=1,2

CV IF DESTRUCTED SHIP CAUGHT IN WEB J: SET "CAUGHT IN A WEB" FLAG OFF
      IF(N+4.EQ.IWEB(J))IWEB(J)=0

CV IF DESTRUCTED SHIP = THOLIANS (N=3) OR KZINTI (N=6): SET WEB(J) OFF
      IF(N.EQ.J*3)IWEB(J)=0

CV NEXT WEB
50    CONTINUE

      RETURN
      END


C - DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C - EREBUS -
C

CV SENSORS REPORT THE DESTRUCTION OF XXXXXX STARBASE

      SUBROUTINE EREBUS
      COMMON /B/NDEAB(2),IPHOB(2),IONB(2),IGOB(2),IFIB(2)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)

CV NEXT INSTRUCTION IS USELESS - HAPPENS ANYWAY; WRONG GOTO LABEL???
CV MAYBE "GOTO 156" TO SKIP STARBASE DESTRUCTION IF LAST PARTICIPANT IN GAME???
      IF(ISTAT.EQ.2)GO TO 163

CV ==============================
CV RESET STARBASE SPECIFIC FLAGS
CV ==============================

CV RESET OWNER PARTY OF STARBASE
CV (= SET DESTRUCTION FLAG)
163   ICHOB(I)=0

CV RESET DEFLECTOR STRENGTH OF STARBASE
      DFLCB(I)=0

CV SET CREW STARBASE OF TO 0
      NDEAB(I)=0

CV RESET "STARBASE HIT BY ION STORM" FLAG
      IONB(I)=0

CV =========================================
CV INFORM M-O SHIPS OF STARBASE DESTRUCTION
CV =========================================

CV LOOP OVER M-O SHIP FOR 1 TO 4
      DO 156 I7=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).EQ.0)GO TO 156

CV CALCULATE FORTRAN UNIT NUMBER OF M-O SHIP
      N=I7+4

CV WRITE MESSAGE "... DESTRUCTION OF <PARTY> STARBASE <NUMBER>"
      WRITE(N,157)(ISIDE(IQ0,I),IQ0=1,3),IBASE(I)
157   FORMAT(' SENSORS REPORT THE DESTRUCTION OF ',3A4,' STARBASE',I3)

CV NEXT M-O SHIP
156   CONTINUE

CV INCREMENT STARBASE NUMBER
      IBASE(I)=IBASE(I)+1
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C - CHARON -
C

CV SENSORS DETECT NO LIFE FORMS ABOARD XXXXXX STARBASE

      SUBROUTINE CHARON
      COMMON /B/NDEAB(2),IPHOB(2),IONB(2),IGOB(2),IFIB(2)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)

CV SET "NO CREW" FLAG
      ICHOB(I)=2
      NDEAB(I)=0
      DO 158 I7=1,4
      IF(ICHOE(I7).EQ.0)GO TO 158
      N=I7+4
      WRITE(N,159)(ISIDE(IQ0,I),IQ0=1,3),IBASE(I)
159   FORMAT(' SENSORS DETECT NO LIFE FORMS ABOARD ',3A4,' STARBASE',I3)
158   CONTINUE
      RETURN
      END

C       -- DONALD ECCLESTONE SUBPROGRAM FOR     T R E K 7
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/200 BY: D.G.
C
C                               - STYX
C

CV DESTRUCTION OF AN M-O SHIP

      SUBROUTINE STYX
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /I/IONK(8),ISPOK(8),ION(4),ISPOT(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /V/IWEB(2),IWEBZ,INVIS(4)
      COMMON /TOM/ITOM

CV CALCULATE RANDOM INDEX OF DECK EXPLODED (IV) IN RANGE 1 TO 33
      CALL RANDO(IV,1,33)

CV CALCULATE M-0 SHIP FORTRAN UNIT NUMBER
      IVV=I+4

CV SET J TO PARTY INDEX
      J=(I+1)/2

2380  WRITE(IVV,238)IBASE(J),(INAME(IQ0,I),IQ0=1,3)
238   FORMAT(' STARBASE',I3,' CALLING ',3A4,'.........'/' AN EXPLOSION H
     *AS OCCURRED IN')

CV WRITE SHIP DECKS HIT
      CALL FORBIN(J,IV,IVV,' ')

      WRITE(IVV,2382)
2382  FORMAT(' AND HAS SPREAD AND ENGULFED YOUR SHIP, DESTROYING YOU'/
     *' AND REDUCING YOUR SHIP TO A LUMP OF MOLTEN SLAG')

CV
      IF(I.EQ.IT)MIN=1

CV IF LAST PARTICIPANTS IN GAME: 164
      IF(ISTAT.EQ.2)GO TO 164

CV REMOVE M-O SHIP FROM MAP
      MAP(IENTR(I),IENTC(I))=IBLK

CV IF ION STORM FLAG SET: RESTORE ION STORM SYMBOL IN MAP
      IF(ISPOT(I).EQ.1)MAP(IENTR(I),IENTC(I))=III

CV
164   DFLCT(I)=0
      NDEAD(I)=0
      IENTR(I)=0
      IENTC(I)=0
      ION(I)=0
      ISPOT(I)=0

CV LOOP OVER M-O SHIPS I7 FOR 1 TO 4
      DO 135 I7=1,4

CV IF LOOPED SHIP IS DESTROYED SHIP: NEXT M-O SHIP
      IF(I7.EQ.I)GO TO 135

      IV=I7+4

CV IF LOOPED SHIP IS IN GAME: WRITE DESTRUCTION MESSAGE
      IF(ICHOE(I7).NE.0)WRITE(IV,136)(INAME(IQ0,I),IQ0=1,3)
136   FORMAT(' SENSORS REPORT THE DESTRUCTION OF THE ',3A4)

CV NEXT M-O SHIP
135   CONTINUE

      IF(IWEB(1).EQ.I)IWEB(1)=0
      IF(IWEB(2).EQ.I)IWEB(2)=0

CV SET I7 TO OLD STATUS OF M-O SHIP I TO BE DESTROYED
      I7=ICHOE(I)

CV SET NEW STATUS OF DESTROYED M-O SHIP I TO "DESTROYED" (3)
      ICHOE(I)=3
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K. 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C - BELIAL -
C

CV CREW REMAINING IS ZERO

      SUBROUTINE BELIAL
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)

      IVV=I+4
      WRITE(IVV,615)
615   FORMAT(' CREW REMAINING IS ZERO'/9X,'THUS THERE IS NO ONE LEFT TO
     * CONTROL THE SHIP')

CV SET NUMBER OF SURVIVORS TO "0"
      NDEAD(I)=0

CV SET "NO CREW" FLAG (2)
      ICHOE(I)=2

CV LOOP OVER M-O SHIPS I7 FOR 1 TO 4
      DO 137 I7=1,4
      IF(I7.EQ.I)GO TO 137
      IV=I7+4
      IF(ICHOE(I7).NE.0)WRITE(IV,136)(INAME(IQ0,I),IQ0=1,3)
136   FORMAT(' SENSORS DETECT NO LIFE FORMS ABOARD THE ',3A4)

CV NEXT M-O SHIP
137   CONTINUE
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C    CONVERTED TO PC BY: DAN GAHLINGER
C      TYPE-EXACT CHECK (W/CONVERSION) 04/25/2000 BY: D.G.
C  - HARLIE
C
C THIS ROUTINE DOES THE DATA FILE ACCESS - NOTE FOR CONVERSION
C
      SUBROUTINE HARLIE
      COMMON /W/IDEX(2,33,20)

C
C ORIGINAL COMMENTED CODE:
C
C     OPEN(UNIT=1,DEVICE='DRA0:',ACCESS='RANDIN',MODE='ASCII',FILE='KIRK.
C    *DAT',DIRECTORY='050030.D.WORK',RECORD SIZE=80)
C     OPEN(UNIT=2,DEVICE='DRA0:',ACCESS='RANDIN',MODE='ASCII',FILE='KOLOTH
C    *.DAT',DIRECTORY='050030.D.WORK',RECORD SIZE=80)
C
C ORIGINAL CODE:
C
C     OPEN(UNIT=1,TYPE='OLD',ACCESS='DIRECT',
C    *FORM='FORMATTED',
C    *RECORDTYPE='FIXED',RECORDSIZE=80,READONLY,
C    *ORGANIZATION='RELATIVE',NAME='SYS$USERS:KIRK.DAT')
C
C     OPEN(UNIT=2,TYPE='OLD',ACCESS='DIRECT',
C    *FORM='FORMATTED',
C    *RECORDTYPE='FIXED',RECORDSIZE=80,READONLY,
C    *ORGANIZATION='RELATIVE',NAME='SYS$USERS:KOLOTH.DAT')
C
      OPEN(UNIT=1,TYPE='OLD',READONLY,
     *ORGANIZATION='SEQUENTIAL',NAME='KIRK.DAT')
      OPEN(UNIT=2,TYPE='OLD',READONLY,
     *ORGANIZATION='SEQUENTIAL',NAME='KOLOTH.DAT')
      DO 1 I=1,33
C      READ(1'I,10)(IDEX(1,I,J),J=1,20)
C 1     READ(2'I,10)(IDEX(2,I,J),J=1,20)
      READ(1,10)(IDEX(1,I,J),J=1,20)
1     READ(2,10)(IDEX(2,I,J),J=1,20)
10    FORMAT(20A4)
C CLOSE THE FILES SO THAT SOME OTHER JOE MIGHT PLAY
      CLOSE(1)
      CLOSE(2)
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/25/2000 BY: D.G.
C
C - FIZBIN -
C

CV SETTING TERMINALS, CHOOSING OF ENEMIES AND NUMBER OF STARS, AND ???

      SUBROUTINE FIZBIN
      INTEGER LAP(3600),LA(132),LK(56),IGOM(8)
      EQUIVALENCE(LAP,MAP),(LA,MA),(LK,K)
C***
      INTEGER ITOM2,IDEV(2)
      INTEGER*2 ICNT
C USED BY VAX VERSION

      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /B/NDEAB(2),IPHOB(2),IONB(2),IGOB(2),IFIB(2)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /F/WARP(4),ITROW(4),ITCOL(4)
      COMMON /G/ZAP(4),ICOLA(4),IROWA(4),LOCK(4),ICOIL(4)
      COMMON /H/ANGLE(4),RANG(4),LOCKT(4)
      COMMON /I/IONK(8),ISPOK(8),ION(4),ISPOT(4)
      COMMON /J/KODE(2,8),STATIC(4)
      COMMON /K/IWHO(8),DIST(8),KILLZ,KILLR,KILLD,KILLG,IGOCO(8)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /O/MA(4,33),K(4,14),NOMAP(4),MANUM(4),HIVEL(4,2),ITEMP(4),
     *NOMOV(4)
      COMMON /P/IPULL(4),IPUSH(4),PULL(4),PUSH(4),IPULLR(4),IPULLC(4),
     *IPUSHR(4),IPUSHC(4)
      COMMON /Q/IARMZ(6),INDUZ,IXRYZ,IMAGZ,IMAGRZ,IABSZ,ISTAZ,INVIZ(4),
     *ICLOZ,IONNO,IPLANZ,IRUNZ,IDEVZ,IDRIZ,IMAXZ
      COMMON /R/IBPSC(4),IBPSB(8),IBPSS(2),IBPSE(25)
      COMMON /S/IBPOB(8),IBPOE(25)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)
      COMMON /V/IWEB(2),IWEBZ,INVIS(4)
      COMMON /W/IDEX(2,33,20)
      COMMON /TOM/ITOM

CV CALCULATE RANDOM STARBASE NUMBERS FOR IBASE(1) AND IBASE(2) (RANGE: 1-17)
94    CALL RANDO(IBASE(1),1,17)
      CALL RANDO(IBASE(2),1,17)

CV IF BOTH STARBASE NUMBERS ARE EQUAL, REPEAT NUMBER GENERATION
      IF(IBASE(1).EQ.IBASE(2))GO TO 94

      CALL TIMEX
      CALL TIMEX
      LAUNCH=0
      ISHAK=0
      NUMOUT=0
      DO 8910 I=1,25
      IGLER(I)=0
      IGLEC(I)=0
      IBPSE(I)=0
8910  IBPOE(I)=0
      ISTAT=0
      MIN=0
CV CHANGED UNIT FROM L TO 5
105   WRITE(5,104)
104   FORMAT(' ENTER THE MANUALLY-OPERATED SHIPS YOU WISH BY ENTERING A
     * STRING CONTAINING-'/' ''E'' FOR ENTERPRISE, ''P'' FOR POTEMPKIN
     * (FEDERATION),'/' ''H'' FOR HAVOC, ''C'' FOR CARNAGE (KLINGON)')
CV CHANGED UNIT FROM L TO 5
      READ(5,332)(IGOM(I),I=1,4)
332   FORMAT(4A1)

107   DO 108 I=1,4
      ICHOE(I)=0

      DO 108 J=1,4

      IF(IGOM(J).NE.IEE(I))GO TO 108

CV SET M-O SHIP IN GAME FLAG
      ICHOE(I)=1

CV INCREMENT NUMBER OF M-O SHIPS IN GAME
      ISTAT=ISTAT+1

CV
108   CONTINUE

CV OLD DEBUGGING OUTPUT
CV      TYPE 11122,ISTAT,ICHOE(1),IGOM(1),I
CV11122 FORMAT(//' 108 ISTAT=',I4,' ICHOE(1)=',I4,' IGOM(1)=',A1,' I=',I4)

CV IF NO M-O SHIP IN GAME: RE-ASK FOR M-O SHIPS
      IF(ISTAT.EQ.0)GO TO 105

CV IF NUMBER OF M-O SHIPS = 1 AND IF M-O SHIP IN GAME:
CV SKIP "ASK FOR TERMINALS FOR OTHER M-O SHIPS"
      IF(ISTAT.EQ.1.AND.ICHOE(1).EQ.1)GO TO 111

CV ASK FOR TERMINALS FOR OTHER M-O SHIPS
CV CHANGED UNIT FROM L TO 5
      WRITE(5,109)

CV OLD PDP-10 CODE?
C109  FORMAT(' WHEN I STOP TYPING, HIT %C AND ENTER THE FOLLOWING COMMAN
C    *DS-'/' ASSIGN TTYAA 5'/' ASS TTYBB 6'/' ASS TTYCC 7'/' ASS TTY
C    *DD E3'/' . CONTINUE'/' <CR>'//' (WHERE AA, BB, CC, DD ARE THE TTY''S
C    * CONTROLLING THE ENTERPRISE, '/' POTEMPKIN, HAVOC AND CARNAGE RESPE
C    *CTIVELY)'/' (IF YOU''RE NOT PLAYING SOME OF THE SHIPS, DON''T BOTH
C    *ER'/' ENTERING THEIR ASSIGN COMMANDS)'/' (IF YOU''VE HAD THE FORES
C    *IGHT TO DO ALL THIS, JUST HIT <CR>)')
C     READ(L,92)NA
C92   FORMAT(A1)

109   FORMAT(' IT IS TIME TO ASSIGN TERMINALS TO TREK7. TO DO
     *THIS YOU NEED TO KNOW'/' THE DEVICE NAME OF THE TERMINALS
     *THAT YOU WILL BE USING.'/' IF YOU DO NOT KNOW THIS THEN
     *STOP THE GAME AND FIND OUT.'/' TO DO THIS TYPE "SHOW TERM",
     *THE FIRST PIECE OF INFORMATION IS THE NAME OF OUR TERMINAL'/'
     *FOR EXAMPLE "TUB1:". FIND OUT THE NAME OF ALL THE TERMINALS
     *'/' AND THEN RESTART THE GAME.')
C***VAX VERSION
C

CV ICNT: LENGTH OF TERMINAL NAME,
CV EG.: TTA1: - TOO SHORT FOR EG.: TTA10: ???
      ICNT=5
      DO 926 ITOM2=1,4
      IF(IGOM(ITOM2).EQ.'E')GOTO 921
      IF(IGOM(ITOM2).EQ.'P')GOTO 922
      IF(IGOM(ITOM2).EQ.'H')GOTO 923
      IF(IGOM(ITOM2).EQ.'C')GOTO 924
      GOTO 926
C
C
CV CHANGED UNIT FROM L TO 5
921   WRITE(5,9211)
9211  FORMAT(' IF YOU WANT THE ENTERPRISE TO PLAY OFF OF THIS
     *TERMINAL THEN TYPE A BLANK.'/' DO NOT TYPE IN THE NAME OF
     *THIS TERMINAL, THIS CAUSES THE GAME TO DIE.')

CV CHANGED UNIT FROM L TO 5
      READ(5,9212)(IDEV(J),J=1,2)
9212  FORMAT(2(A4))

      IF(IDEV(1).EQ.' ')GOTO 926

      CALL ASSIGN(5,IDEV,ICNT)
      GOTO 926
C
C
CV CHANGED UNIT FROM L TO 5
922   WRITE(5,9221)
9221  FORMAT(' THE POTEMPKIN IS ASSIGNED TO TERMINAL ',$)

CV CHANGED UNIT FROM L TO 5
      READ(5,9222)(IDEV(J),J=1,2)
9222  FORMAT(2(A4))

      CALL ASSIGN(6,IDEV,ICNT)
      GOTO 926
C
C
CV CHANGED UNIT FROM L TO 5
923   WRITE(5,9231)
9231  FORMAT(' THE HAVOC IS ASSIGNED TO TERMINAL ',$)
CV CHANGED UNIT FROM L TO 5
      READ(5,9232)(IDEV(J),J=1,2)
9232  FORMAT(2(A4))

      CALL ASSIGN(7,IDEV,ICNT)
      GOTO 926
C
C
CV CHANGED UNIT FROM L TO 5
924   WRITE(5,9241)
9241  FORMAT(' THE CARNAGE IS ASSIGNED TO TERMINAL ',$)
CV CHANGED UNIT FROM L TO 5
      READ(5,9242)(IDEV(J),J=1,2)
9242  FORMAT(2(A4))

      CALL ASSIGN(8,IDEV,ICNT)
C
C
926   CONTINUE

CV =================================
CV SET INITIAL VALUES FOR M-O SHIPS
CV =================================

111   DO 110 I=1,4
      IF(ICHOE(I).EQ.0)GO TO 110
      IBPSC(I)=I
      ISPOT(I)=0
      ION(I)=0
      ZAP(I)=0
      INVIS(I)=0
      TWARP(I)=10.
      LOCK(I)=-1
      LOCKT(I)=-1
      ICOIL(I)=0
      WARP(I)=0.
      NOMOV(I)=0
      IPHOT(I)=12
      PHASR(I)=6000.
      DFLCT(I)=100.
      ISURR(I)=0
      RANG(I)=10.
      NOMAP(I)=15
      MANUM(I)=0
      ITEMP(I)=4000
      NDEAD(I)=430
      NOMOV(I)=0

CV CALCULATE ROW POSITION OF SHIP WITH INDEX "I"
      CALL RANDO(IENTR(I),1,60)

CV CALCULATE COLUMN POSITION OF SHIP WITH INDEX "I"
      CALL RANDO(IENTC(I),1,60)
      LI2(I)=0

110   CONTINUE

CV ===============================
CV SET INITIAL VALUES FOR PARTIES
CV ===============================

      I3=0

CV LOOP OVER ALL PARTIES
      DO 120 I=1,2

CV SET TOTAL NUMBER OF PARTICIPANTS OF PARTY I TO "0"
      II(I)=0

CV SET EAGLE OF I OWNER PARTY TO "0"
      NUME(I)=0

CV SET STARBASE OUT OF GAME
      ICHOB(I)=0

CV SET STARBASE OUT OF ION STORM
      IONB(I)=0

CV SET STARBASE OWNER PARTY INDEX TO I ???
CV 1=ENTERPRISE
CV 2=POTEMPKIN
CV OR OTHERS IF SUCCESSFULLY BOARDED?
      IBPSS(I)=I

CV STARBASE CREW = 1000
      NDEAB(I)=1000

CV STARBASE NUMBER OF AVAILABLE TORPEDOES = 20
      IPHOB(I)=20

CV SET STARBASE NOT CAUGHT IN WEB
      IWEB(I)=0

CV RESET "STARBASE WAS HIT" FLAG (0=NOT HIT)
      IGOB(I)=0

CV SET "STARBASE CEASE FIRE FLAG" (0=ATTACK)
      IFIB(I)=0

CV STARBASE DEFLECTOR STRENGTH = 300
120   DFLCB(I)=300.

CV ====================
CV SET SPECIFIC VALUES
CV ====================

CV RESET KZINTI SPECIFIC FLAGS
      INDUZ=0
      IXRYZ=0
      IWEBZ=0
      ISTAZ=0

CV SET ROMULAN STRATEGY "KILLR" TO "NEUTRAL" (1)
      KILLR=1

CV SET GORN STRATEGY "KILLG" TO "ATTACK" (1)
      KILLG=1

CV
      KILLD=1

CV
      IONNO=0

CV =====================
CV CHOOSING OPPONENT(S)
CV =====================

CV CALCULATE RANDOM M-O OPPONENT(S) CHOOSER (I7)
112   CALL RANDO(I7,1,4)

CV IF M-O CHOOSER NOT IN GAME: RECALCULATE ... (112)
      IF(ICHOE(I7).EQ.0)GO TO 112

CV CALCULATE M-O CHOOSER FORTRAN UNIT NUMBER
      L=I7+4

CV LOOP OVER ALL M-O SHIPS
CV = FIND M-O SHIPS TO WAIT FOR CHOOSER
      DO 113 I=1,4

CV IF M-O SHIP NOT IN GAME OR M-O IS CHOOSER: NEXT M-O SHIP
      IF(I.EQ.I7.OR.ICHOE(I).EQ.0)GO TO 113

CV CALCULATE M-O NO-CHOOSER FORTRAN UNIT NUMBER
      I8=I+4

CV WRITE NO-CHOOSER MESSAGE
      WRITE(I8,114)(INAME(IQ0,I7),IQ0=1,3)
114   FORMAT(' PLEASE WAIT WHILE THE ',3A4,' CHOOSES THE ENEMIES')

CV NEXT M-O SHIP
113   CONTINUE

CV WRITE CHOOSER MESSAGE
      WRITE(L,751)
751   FORMAT(' PICK THE OPPONENT(S) YOU WISH TO FIGHT BY TYPING IN A STR
     *ING OF LETTERS-'/' EACH LETTER CORRESPONDING TO THE OPPONENT YOU W
     *ISH TO BATTLE.'/' TYPE ''K'' FOR KLINGONS, ''R'' FOR ROMULANS'/'
     *''T'' FOR THOLIANS, ''G'' FOR GORNS'/' ''O'' FOR ORIONS, ''Z'' FOR
     * KZINTI'/' ''D'' FOR A DOOMSDAY MACHINE'/' AND/OR ''A'' FOR MOONBA
     *SE ALPHA')

CV READ CHOSEN C-O OPPONENT(S) IGOM(I)
      READ(L,752)(IGOM(I),I=1,8)
752   FORMAT(8A1)

CV ==================
CV PREPARE C-O SHIPS
CV ==================

CV LOOP OVER ALL C-O SHIPS
      DO 7531 I=1,8

CV SET C-O SHIP DEFLECTOR STRENGTH TO 100
      DFLCK(I)=100.

CV SET NUMBER OF STARBASE BOARDERS TO NONE (0) ?
      IBPOB(I)=0

CV SET FOREIGN OWNER FLAG TO "0"
      IBPSB(I)=0

CV SET C-O SHIP "IN GAME" FLAG TO "NO" (0)
      ICHOS(I)=0

CV SET C-O SHIP "IN ION STORM" FLAG TO "NO" (0)
      ISPOK(I)=0

CV SET C-O SHIP "HIT BY ION STORM" FLAG TO "NO" (0)
      IONK(I)=0

CV SET GO FLAG TO = 1 ???
      IGOCO(I)=1

CV =========================
CV PRCOESS CHOSEN C-O SHIPS
CV =========================

CV LOOP OVER ALL CHOSEN C-O OPPONENTS
      DO 753 J=1,8

CV IF CHOSEN C-O OPPONENT <> CURRENT C-O SHIP: SKIP SETTING C-O SHIP IN GAME
      IF(IGOM(J).NE.IENM1(I))GO TO 753

CV ELSE: SET CURRENT (I) C-O SHIP IN GAME
      ICHOS(I)=1

CV NEXT CHOSEN C-O OPPONENT
753   CONTINUE

CV NEXT C-O SHIP
7531  CONTINUE

CV SET MOONBASE DEFLECTOR STRENGTH TO 70
      DFLCK(8)=70.

CV IF KZINTI IS IN GAME: CONFIGURE KZINTI
      IF(ICHOS(6).EQ.1)CALL KZIN

CV IF "SAME MAP" FLAG (?) IS SET: SKIP GENERATE ION STORMS
      IF(IGO(1).EQ.1)GO TO 1260

CV ====================
CV GENERATE ION STORMS
CV ====================

CV BLANK COMPLETE MAP

CV LOOP OVER ALL MAP POSITIONS (I)
      DO 501 I=1,3600

CV SET LAP(I) TO BLANK
501   LAP(I)=IBLK

CV CALCULATE RANDOM IV IN RANGE 0-10
      CALL RANDO(IV,0,10)

CV IF I=0 (10% PROBABILITY): SKIP GENERATE ION STORMS
      IF(IV.EQ.0)GO TO 1260

CV ELSE (90% PROBABILITY): DO GENERATE ION STORMS

CV LOOP OVER NUMBER OF ION STORM GENERATIONS (N)
CV IN RANGE 1-IV (RANDOM RANGE 1-10)
      DO 647 N=1,IV

CV CALCULATE RANDOM CELL EXTENT (IVV) IN RANGE 4-11
CV THE CELL EXTENT IS FOR BOTH ROWS AND COLUMNS,
CV SO ION STORMS ARE ALWAYS SQUARE IN SIZE
648   CALL RANDO(IVV,4,11)

CV CALCULATE RANDOM START ROW (MIN) IN RANGE 1-60
      CALL RANDO(MIN,1,60)

CALCULATE RANDOM START COLUMN (J) IN RANGE 1-60
      CALL RANDO(J,1,60)

CV END ROW = START ROW + CELL EXTENT
      I7=MIN+IVV

CV END COL = START COL + CELL EXTENT
      I8=J+IVV

CV IF END ROW (I7) > 60: RECALCULATE IVV, MIN & J
      IF(I7.GT.60)GO TO 648

CV IF END COL (I8) > 60: RECALCULATE IVV, MIN & J
      IF(I8.GT.60)GO TO 648

CV LOOP OVER COL (IVV) IN RANGE J-I8
      DO 649 IVV=J,I8

CV LOOP OVER ROW (I) IN RANGE MIN-I7
      DO 649 I=MIN,I7

CV SET ION STORM TO POSITION ROW=I/COL=IVV
649   MAP(I,IVV)=III

CV NEXT ION STORM GENERATION
647   CONTINUE

CV REMARK: ION STORMS CAN OVERLAP

CV ====================

CV SET TOTAL NUMBER OF FEDERATION PARTICIPANTS TO
CV INGAME(ENTERPRISE) + INGAME(POTEMPKIN)
1260  II(1)=ICHOE(1)+ICHOE(2)

CV SET TOTAL NUMBER OF KLINGON PARTICIPANTS TO
CV INGAME(HAVOC) + INGAME(CARNAGE)
      II(2)=ICHOE(3)+ICHOE(4)

CV SET NUMBER OF FEDERATION SHIPS TO NUMBER OF FEDERATION PARTICIPANTS
      IJ(1)=II(1)

CV SET NUMBER OF KLINGON SHIPS TO NUMBER OF KLINGON PARTICIPANTS
      IJ(2)=II(2)

CV =========================
CV STARBASE WISHING/VETOING
CV =========================

CV GET NUMBER OF C-O SHIP IN GAME INTO I3

CV LOOP OVER C-O SHIPS I FOR 1 TO 8
      DO 115 I=1,8

CV IF C-O SHIP IN GAME INCREMENT NUMBER OF C-O SHIPS IN GAME (I3),
CV NEXT C-O SHIP
115   IF(ICHOS(I).EQ.1)I3=I3+1

CV IMPLEMENTATION OF RULES FOR STARBASE WISHING/VETOING

CV LOOP OVER PARTIES IVV FOR 1 TO 2 (IVV = 1: FEDERATION / 2: KLINGON)
      DO 119 IVV=1,2

CV J IS OPPONENT SIDE - 1=FEDERATION/2=KLINGON

CV IVV=1 => J=2
CV IVV=2 => J=1
      J=3-IVV

CV IF TOTAL NUMBER OF PARTY (IVV) PARTICIPANTS = 0 OR
CV TOTAL NUMBER OF OPPPONENT PARTY (J) PARTICIPANTS <> 0:
CV SKIP STARBASE WISHING/VETOING
      IF(II(IVV).EQ.0.OR.II(J).NE.0)GO TO 119

CV STARBASE WISHING/VETOING FOR OPPPONENT PARTY (J)
      CALL MENDEZ(J)

CV NEXT PARTY
119   CONTINUE

CV LOOP OVER PARTIES IVV FOR 1 TO 2 (IVV = 1: FEDERATION / 2: KLINGON)
      DO 116 IVV=1,2

CV IF TOTAL NUMBER OF PARTY (IVV) PARTICIPANTS = 0:
CV SKIP STARBASE WISHING/VETOING
      IF(II(IVV).EQ.0)GO TO 116

CV IF PARTY ALREADY HAS STARBASE: SKIP STARBASE WISHING/VETOING
      IF(ICHOB(IVV).NE.0)GO TO 116

CV IF TOTAL NUMBER OF OPPONENT C-O PARTY <> NUMBER OF STARBASE (0/1)
CV (THERE IS AT LEAST ONE C-O SHIP MORE THAN NUMBER OF STARBASE?):
CV DO STARBASE WISHING/VETOING
CV #-TOTAL  #-BASE  WISH/VETO
CV    1        0       YES
CV    1        1        NO
CV    2        0       YES
CV    2        1       YES
CV    3        O       YES
CV    3        1       YES
      IF(II(3-IVV).NE.ICHOB(3-IVV))GO TO 117

CV IF 3 * TOTAL NUMBER OF PARTY PARTICIPANTS > NUMBER OF C-O SHIPS IN GAME:
CV SKIP STARBASE WISHING/VETOING
      IF(3*II(IVV).GT.I3)GO TO 116

CV STARBASE WISHING/VETOING FOR PARTY (IVV)
117   CALL MENDEZ(IVV)

CV NEXT PARTY
116   CONTINUE

CV ===========================
CV SHAKEDOWN CRUISE DETECTION
CV ===========================

CV IF NO KLINGON STARBASE (2) AND NUMBER OF C-O SHIPS IN GAME(I3)=0:
CV SHAKEDOWN CRUISE
      IF(II(2).EQ.0.AND.I3.EQ.0)ISHAK=1

CV IF NO FEDERATION STARBASE(1) AND NUMBER OF C-O SHIPS IN GAME(I3)=0:
CV SHAKEDOWN CRUISE
      IF(II(1).EQ.0.AND.I3.EQ.0)ISHAK=1

CV IF NO SHAKEDOWN CRUISE: SKIP ANNOUNCE SHAKEDOWN CRUISE
      IF(ISHAK.EQ.0)GO TO 126

CV LOOP OVER ALL M-O SHIPS
      DO 122 J=1,4

CV CALCULATE M-O SHIP FORTRAN UNIT NUMBER
      I=J+4

CV IF M-O SHIP IS IN GAME: WRITE SHAKEDOWN MESSAGE
      IF(ICHOE(J).EQ.1)WRITE(I,6100)
6100  FORMAT(' BEGINNING SHAKEDOWN CRUISE')

CV NEXT M-O SHIP
122   CONTINUE

CV ===========================

CV IF SAME MAP: SKIP QUERY AND GENERATE STARS (5020)
126   IF(IGO(1).EQ.1)GO TO 5020

CV =========================
CV QUERY AND GENERATE STARS
CV =========================

CV ASK FOR NUMBER OF STARS
      WRITE(L,610)
610   FORMAT(' HOW MANY STARS DO YOU WISH IN YOUR 60 X 60 FIELD - BETWEE
     *N 0 AND 500')

CV
      GO TO 655

CV MANAGE PDP-10 SPECIAL (NOT NEEDED FOR VAX/VMS;
CV INCLUDES IMMEDIATE RETURN)
1262  CALL ILLDAT
      GO TO 126

CV READ NUMBER OF STARS
655   READ(L,*,ERR=1262)DISTP

CV IF NUMBER OF START >1500 (WHY NOT 500?
CV CHECKED IN SOURCE: O.K.): RE-ASK
1261  IF(DISTP.GT.1500.)GO TO 126

CV UNUSED FORMAT, PROBABLY FOR READ AT 655
24    FORMAT(F5.0)

CV SET I7 TO INTEGER OF NUMBER OF STARS
123   I7=IFIX(DISTP)

CV IF I7<0: RE-ASK
      IF(I7.LT.0)GO TO 126

CV SET NUMBER OF DILITHIUM STARS TO 0
      ISTAT=0

CV IF NUMBER OF STARS = 0: SKIP GENERATE STARS
      IF(I7.EQ.0)GO TO 5022

CV GENERATE STARS

CV LOOP OVER I IN RANGE 1-<NUMBER OF STARS>
      DO 502 I=1,I7

CV CALCULATE RANDOM ROW (IV) IN RANGE 1-60
      CALL RANDO(IV,1,60)

CV CALCULATE RANDOM COL (IVV) IN RANGE 1-60
      CALL RANDO(IVV,1,60)

CV IF NUMBER OF DILITHIUM STARS>=5: SKIP ADD DILITHIUM STAR
      IF(ISTAT.GE.5)GO TO 502

CV CALCULATE RANDOM J IN RANGE 1-100
      CALL RANDO(J,1,100)

CV IF J>5 SKIP ADD DILITHIUM STAR
      IF(J.GT.5)GO TO 502

CV ELSE: ADD DILITHIUM STAR (5% PROBABILITY)

CV INCREMENT NUMBER OF DILITHIUM STARS
      ISTAT=ISTAT+1

CV SET DILITHIUM STAR FLAGS
      LI2R(ISTAT)=IV
      LI2C(ISTAT)=IVV

CV PLACE STAR IN MAP, NEXT STAR
502   MAP(IV,IVV)=ISTAR

CV IF MORE THAN 5 DILITHIUM STARS: 5020
5022  IF(ISTAT.GE.5)GO TO 5020

CV ELSE: LESS THAN 5 DILITHIUM STARS

CV LOOP OVER I IN RANGE <NUMBER OF DILITHIUM STARS>+1-5
      DO 5021 I=ISTAT+1,5

CV RESET DILITHIUM STAR FLAGS
      LI2R(I)=0
5021  LI2C(I)=0

CV =======================
CV PUT M-O SHIPS INTO MAP
CV =======================

CV LOOP OVER ALL M-O SHIPS
5020  DO 125 I=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP (125)
      IF(ICHOE(I).EQ.0)GO TO 125

CV IF POSITION OF M-O SHIP IS NOT ION STORM: 1250
      IF(MAP(IENTR(I),IENTC(I)).NE.III)GO TO 1250

CV ELSE: POSITION OF M-O SHIP IS ION STORM

CV SET "M-O SHIP IN ION STORM" FLAG
      ISPOT(I)=1

CV SET "M-O SHIP HIT BY ION STORM" FLAG
      ION(I)=1

CV SET MAP ENTRY TO M-O SHIP TYPE
1250  MAP(IENTR(I),IENTC(I))=IEE(I)

CV NEXT M-O SHIP
125   CONTINUE

CV =======================
CV PUT C-O SHIPS INTO MAP
CV =======================

CV LOOP OVER ALL C-O-SHIPS
      DO 754 I=1,8

CV SET C-0 SHIP POSITION TO 0/0 (= OUT OF GAME)
      IKLNR(I)=0
      IKLNC(I)=0

CV IF C-O SHIP NOT IN GAME: NEXT C-O SHIP
      IF(ICHOS(I).NE.1)GO TO 754

CV CALCULATE RANDOM ROW POSITION (I7) / COL POSITION (I8)
7501  CALL RANDO(I7,2,59)
      CALL RANDO(I8,2,59)

CV PUT MAP CONTENT OF I7/I8 INTO NA
      NA=MAP(I7,I8)

CV IF NA IS NEITHER "BLANK" NOR "STAR" NOR "ION STORM": 7501
      IF(NA.NE.IBLK.AND.NA.NE.ISTAR.AND.NA.NE.III)GO TO 7501

CV IF SHIP IS MOONBASE AND NA IS NOT "ION STORM": 7501
      IF(I.EQ.8.AND.NA.EQ.III)GO TO 7501

CV ELSE: PLACE C-O SHIP(I) AT I7/I8
      IKLNR(I)=I7
      IKLNC(I)=I8

CV IF SHIP(I) IS NOT KZINTI OR KZINTI NOT IN GAME:
CV SKIP PREPARE FOR KZINTI
7502  IF(I.NE.6.OR.ICHOS(6).EQ.0)GO TO 750

CV ELSE THERE IS A KZINTI SHIP

CV PREPARE FOR KZINTI
      CALL NIVEN

CV NEXT C-O SHIP
      GO TO 754

CV SET ION STORM FLAGS
750   CALL ASIMOV(NA,I)

CV SET M-O SHIP TYPE IN MAP (EXCEPT FOR KZINTI)
151   MAP(I7,I8)=IENM1(I)

CV NEXT C-O SHIP
754   CONTINUE

CV ============================
CV SET ROMULAN SHIP VISIBILITY
CV ============================

CV IF NO ROMULAN SHIP IN GAME: SKIP SET ROMULAN SHIP VISIBILITY
      IF(ICHOS(2).EQ.0)GO TO 890

CV LOOP OVER ALL M-O SHIPS
      DO 127 I=1,4

CV IF M-O SHIP NOT IN GAME: NEXT SHIP
      IF(ICHOE(I).EQ.0)GO TO 127

CV IF DISTANCE BETWEEN ROMULAN SHIP AND M-O SHIP >=5:
CV SKIP SET ROMULAN SHIP VISIBLE
      IF(DI(IKLNC(2),IKLNR(2),IENTC(I),IENTR(I)).GE.5)GO TO 127

CV ELSE: DISTANCE <5: SET ROMULAN SHIP VISIBLE FOR M-O SHIP
      INVIS(I)=1

CV NEXT M-O SHIP
127   CONTINUE

CV ===========================
CV SET KZINTI SHIP VISIBILITY
CV ===========================

CV IF NO KZINTI CLOAKING DEVICE: SKIP SET KZINTI SHIP VISIBILITY
890   IF(IARMZ(5).EQ.0)GO TO 89

CV IF NO KZINTI IN GAME: SKIP SET KZINTI SHIP VISIBILITY
      IF(ICHOS(6).EQ.0)GO TO 89

CV LOOP OVER ALL M-O SHIPS
      DO 891 I=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I).EQ.0)GO TO 891

CV IF DISTANCE BETWEEN KZINTI SHIP AND M-O SHIP >=ICLOZ:
CV SKIP SET KZINTI SHIP VISIBLE
      IF(DI(IKLNC(6),IKLNR(6),IENTC(I),IENTR(I)).GE.ICLOZ)GO TO 891

CV SET KZINTI SHIP VISIBLE
      INVIZ(I)=1

CV NEXT M-O SHIP
891   CONTINUE

CV ==================================

CV ???
89    ISTAT=0

CV =======================
CV BROKEN CODE MANAGEMENT
CV =======================

CV LOOP OVER ALL CODES
      DO 170 I=1,8

CV RESET "BROKEN CODE" FLAGS TO "0" (NO)

CV RESET "BROKEN CODE" FLAG FOR FEDERATION PARTY
      KODE(1,I)=0

CV RESET "BROKEN CODE" FLAG FOR KLINGON PARTY
170   KODE(2,I)=0

CV LOOP OVER ALL OPPONENT PARTIES (I) FOR 1-2
      DO 171 I=1,2

CV CALCULATE RANDOM BROKEN CODE FACTOR (IV) IN RANGE 0-2
      CALL RANDO(IV,0,2)

CV IF IV = 0: SKIP CALCULATING BROKEN CODE
      IF(IV.EQ.0)GO TO 171

CV ELSE: CALCULATE RANDOM BROKEN CODE NUMBER (I8) IN RANGE 1-8
      CALL RANDO(I8,1,8)

CV SET BROKEN CODE FOR OPPONENT PARTY (I) AND CODE (I8)
      KODE(I,I8)=1

CV CALCULATE RANDOM "BROKEN CODE MESSAGE" FACTOR (JTK) IN RANGE 0-2
      CALL RANDO(JTK,0,2)

CV IF "BROKEN CODE MESSAGE" FACTOR (JTK) <> 0:
CV SKIP MESSAGE "... BROKEN CODE"
      IF(JTK.NE.0)GO TO 171

CV IF I=1: IVV=2 / I=2: IVV=4
      IVV=2*I

CV INFORM M-O SHIPS ABOUT BROKEN CODE

CV LOOP OVER MIN IN RANGE IVV-1 TO IVV (1-2 / 3-4)
      DO 1710 MIN=IVV-1,IVV

CV IF M-O SHIP NOT IN GAME: SKIP WRITE MESSAGE "... BROKEN CODE"
      IF(ICHOE(MIN).EQ.0)GO TO 1710

CV CALCULATE M-O SHIP FORTRAN UNIT NUMBER
      J=MIN+4

CV WRITE MESSAGE "... BROKEN CODE"
      WRITE(J,172)I8
172   FORMAT(' INTELLIGENCE REPORTS THAT THE ENEMY HAVE BROKEN CODE',I2)

CV NEXT MIN
1710  CONTINUE

CV NEXT M-O SHIP (I)
171   CONTINUE

CV ==============================================
CV LA(X) AND LK(X) NEVER USED ANYWHEWRE ELSE ...
CV ==============================================

CV LOOP OVER I IN RANGE 1 TO 56 ???
      DO 118 I=1,56
      LA(I)=0
118   LK(I)=0

CV LOOP OVER I IN RANGE 57 TO 132 ???
      DO 747 I=57,132
747   LA(I)=0

CV ==============
CV MANAGE EAGLES
CV ==============

CV IF NO MOONBASE IN GAME: SKIP MANAGE EAGLES
      IF(ICHOS(8).NE.1)GO TO 8900

CV MANAGE EAGLES
      CALL LEGUIN(0,0)

8900  RETURN
      END



C - TAKES   A LICKIN' BUT KEEPS ON TICKIN'
C CONVETED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G. (JOKE - THIS NEVER GETS USED!)
C
C  TIMEX
C

CV TIME CHECK/ACCESS PASSWORD

      SUBROUTINE TIMEX
      IMPLICIT INTEGER (A-Z)
      DIMENSION I(2),MON(12),F(12)
C FOLLOWING DATA CREATES AN ERROR SO WE FUDGE IT
C     DATA MON/-28580569024,-30711668672,-26969825216,-33349361600,
C    *-26969595840,-28496682944,-28496748480,-33328750528,-23731888064,
C    *-25887629248,-26374102976,-31785377728/
C HERES MY NEW CODE APR/21/2000
C THIS NEVER GETS USED, SO NOBODY CARES
      DATA MON/9024,8672,5216,1600,
     *5840,2944,8480,528,8064,
     *9248,2976,7728/
      DATA F/0,3,3,6,1,4,6,2,5,0,3,5/
C NEW FORTRAN AND Y2K CHANGE THE NEXT TWO LINES, NO LONGER VALID
C     CALL DATE(I)
C     CALL TIME(D,A)
C    FAKE CODE ENTERED NEXT TWO LINES TO FIX ANOTHER BUG (FAKE BUG)

      D=1

CV BECAUSE D NEVER IS "2": RETURN
      IF(D.EQ.2)GO TO 99

C LINE BELOW CAUSES FALSE ERROR IN TIMEX LINE 18
      RETURN

C ABOVE LINE MAY GENERATE A FALSE ERROR
99    M=I(1)/2 + I(2)/128 + D/2 + A/128 + 2653589

CV SEE: HTTP://BITSAVERS.INFORMATIK.UNI-STUTTGART.DE/WWW.COMPUTER.MUSEUM.
CV UQ.EDU.AU/NEWSLETTERS/WN-112%20COMPUTER%20CENTRE%20WEEKLY%20NEWSLETTER
CV %2020%20OCT%2072.PDF
CV FOR INFO ABOUT SETRAN ROUTINE (PDP-10 SPECIFIC)
C     CALL SETRAN(M)

      DECODE(2,4,D)M
4     FORMAT(I2)

      IF(M.LT.10.OR.M.GT.16)RETURN

      DECODE(9,1,I)D,A,Y
1     FORMAT(I2,1X,A3,1X,I2)

      Y=Y+1900

      DO 2 M=1,12

      IF(A.EQ.MON(M))GO TO 3
2     CONTINUE

3     IF(M.GE.4.AND.M.LE.9)RETURN
      LL=0
      Y=Y-400*(Y/400)
      A=Y+(Y+3)/4-(Y-1)/100

      IF(Y.EQ.(Y/4)*4.AND.M.GE.3)LL=1
      A=A+D+LL+F(M)
      A=A-(A/7)*7

      IF(A.EQ.1.OR.A.EQ.22)RETURN
      L=5

      CALL RANDO(A,1,7)
      GO TO (9,10,12,14,16,18,20),A
14    WRITE(L,15)
15    FORMAT(' AS PASSWORD ENTER NAME OF THE DISCOVERER OF THE SPACE
     * WARP')
      GO TO 8
12    WRITE(L,13)
13    FORMAT(' AS PASSWORD ENTER NAME OF ENTERPRISE''S 1ST CAPTAIN')
      GO TO 8
10    WRITE(L,11)
11    FORMAT(' AS PASSWORD ENTER KIRK''S MIDDLE NAME')
      GO TO 8
9     WRITE(L,6)
6     FORMAT(' AS PASSWORD ENTER NAMES OF SIGNERS OF ORGANIAN PEACE
     * TREATY')
      GO TO 8
16    WRITE(L,17)
17    FORMAT(' AS PASSWORD ENTER MUDD''S FULL NAME')
      GO TO 8
18    WRITE(L,19)
19    FORMAT('AS PASSWORD ENTER NAME OF SHIP WITH STAR FLEET ID NUMBER
     * NCC-3810')
      GO TO 8
20    WRITE(L,21)
21    FORMAT(' AS PASSWORD ENTER WEIGHT OF ENTERPRISE IN CARATS')
8     READ(L,7)Y,A,D,M,LL,I
7     FORMAT(7A5)
      WRITE(5,5)
5     FORMAT(' SORRY - INCORRECT PASSWORD-'/' IN ACCORDANCE WITH THE UNI
     *VERSITY COMPUTING COUNCIL''S POLICY'/' (SEE HLP:RESTRI.MEM), TREK7
     * CANNOT BE ACCESSED ON WEEKDAYS'/' BETWEEN THE HOURS OF 1000 AND 1
     *700.'/' COME BACK TONIGHT AND PLAY IT WITHOUT THESE
     * RESTRICTIONS.')
      STOP
      END


C       -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C               - MENDEZ

CV STARBASE WISHING/VETOING

      SUBROUTINE MENDEZ(J)
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /B/NDEAB(2),IPHOB(2),IONB(2),IGOB(2),IFIB(2)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
        COMMON /TOM/ITOM

CV SET M-O SHIP COUNTER START (I) TO 1
      I=1

CV SET M-O SHIP COUNTER STOP (N) TO 1
      N=4

CV IF NUMBER OF FEDERATION PARTICIPANTS = 0:
CV SET M-O SHIP COUNTER START (I) TO 3
      IF(II(1).EQ.0)I=3

CV IF NUMBER OF KLINGON PARTICIPANTS = 0:
CV CV SET M-O SHIP COUNTER STOP (N) TO 2
      IF(II(2).EQ.0)N=2

CV IF WISHABLE STARBASE PARTY IS KLINGON AND THE FEDERATION PARTY
CV ALREADY HAS A STARBASE: SET M-O SHIP COUNTER START (I) TO 3
      IF(J.EQ.2.AND.ICHOB(1).EQ.1)I=3

CV LOOP OVER M-O SHIPS (MIN) FOR COUNTER START (I) TO COUNTER STOP (N)
      DO 1 MIN=I,N

CV IF M-O SHIP MIN NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(MIN).EQ.0)GO TO 1

      I8=MIN+4
      WRITE(I8,2)(ISIDE(IQ0,J),IQ0=1,3)
2     FORMAT(' DO YOU WISH A ',3A4,' STARBASE?')
      READ(I8,3)NA
3     FORMAT(A1)

CV IF ANSWER IS NOT "Y": VETOING (4)
      IF(NA.NE.'Y')GO TO 4

CV NEXT M-O SHIP
1     CONTINUE

CV ================
CV CREATE STARBASE
CV ================

CV SET STARBASE IN GAME FLAG
      ICHOB(J)=1

CV GENERATE RANDOM STARBASE X/Y POSITION
      CALL RANDO(IBASR(J),1,60)
      CALL RANDO(IBASC(J),1,60)

CV IF THERE IS AN ION STORM AT THE CALCULATED STARBASE POSITION:
CV SET ION STORM FLAG
      IF(MAP(IBASR(J),IBASC(J)).EQ.III)IONB(J)=1

CV PUT STARBASE INTO CALCULATED POSITION
      MAP(IBASR(J),IBASC(J))=ISTAR

CV INCREMENT NUMBER OF PARTY J PARTICIPANTS
      II(J)=II(J)+1

      RETURN

CV ======================
CV STARBASE VETO MESSAGE
CV ======================

CV LOOP OVER M-O SHIPS IV FOR 1 TO 4
4     DO 5 IV=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(IV).EQ.0)GO TO 5

CV IF M-O SHIP = WISHING M-O SHIP: NEXT M-O SHIP
      IF(IV.EQ.MIN)GO TO 5

      I8=IV+4
      WRITE(I8,6)(ISIDE(IQ0,J),IQ0=1,3),(INAME(IQ0,MIN),IQ0=1,3)
6     FORMAT(1X,3A4,' STARBASE VETOED BY ',3A4)

CV NEXT M-O SHIP
5     CONTINUE
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C - KZIN -
C

CV KZINTI SETUP

      SUBROUTINE KZIN
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /I/IONK(8),ISPOK(8),ION(4),ISPOT(4)
      COMMON /K/IWHO(8),DIST(8),KILLZ,KILLR,KILLD,KILLG,IGOCO(8)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(5),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /Q/IARMZ(6),INDUZ,IXRYZ,IMAGZ,IMAGRZ,IABSZ,ISTAZ,INVIZ(4),
     *ICLOZ,IONNO,IPLANZ,IRUNZ,IDEVZ,IDRIZ,IMAXZ
      COMMON /S/IBPOB(8),IBPOE(25)
      COMMON /V/IWEB(2),IWEBZ,INVIS(4)

CV IF NUMBER OF BOARDERS ON KZINTI SHIP <> 0: RETURN ???
1     IF(IBPOB(6).NE.0)RETURN

      WRITE(L,2)
2     FORMAT(' THE KZINTI ARE A SPECIAL ENEMY- THEY ARE PLAYER PROGRAMMA
     *BLE.'/' THUS THE PLAYER ENTERS PARAMETERS REGARDING THE ENEMY STRE
     *NGTH'/' AND STRATEGY.')
7     WRITE(L,3)
3     FORMAT(//' TYPE OF STRATEGY -- ENTER A NUMBER-'/
     *' ''1'' FOR ATTACK/RETREAT'/
     *' ''2'' FOR KAMIKAZE'/
     *' ''3'' FOR CLOSE PASSES (EG. ORIONS)')
      N=1
      READ(L,9,ERR=73)DISTP

CV SET CHOSEN STRATEGY (IPLANZ) TO INTEGER OF DISTP
      IPLANZ=IFIX(DISTP)

CV IF ANSWER IS ILLEGAL: REPEAT QUESTION (7)
      IF(IPLANZ.LT.1.OR.IPLANZ.GT.3)GO TO 7

CV ======================================
CV ASK STRATEGY ATTACK/RETREAT QUESTIONS
CV ======================================

CV IF ANSWER = 2 OR 3: SKIP STRATEGY ATTACK/RETREAT QUESTIONS (15)
      IF(IPLANZ.NE.1)GO TO 15

10    WRITE(L,8)
8     FORMAT(' ENTER A NUMBER REPRESENTING BY HOW MUCH YOUR
     *'/' DEFLECTORS MUST EXCEED THE KZINTIS'' BEFORE THE KZINTI START'
     */'TO RETREAT.'/' EG. 30 MEANS THE KZINTI RETREAT WHEN YOUR
     * DEFLECTORS ARE'/5X,' GREATER THAN THEIRS BY AT LEAST 30.'/,
     *' EG. -30 MEANS THE KZINTI RETREAT UNLESS THEY EXCEED YOUR
     *'/5X,' DEFLECTORS BY AT LEAST 30.')
      N=2
      READ(L,9,ERR=73)DISTP
9     FORMAT(F7.0)

CV SET CHOSEN DEFLECTOR DISADVANTAGE (IRUNZ) TO INTEGER OF DISTP
      IRUNZ=IFIX(DISTP)

131   WRITE(L,130)
130   FORMAT(' ENTER A NUMBER (IN DEGREES BETWEEN 0 AND 180) REPRESENTI
     *NG'/' THE MAXIMUM ANGLE THE KZINTIS7 PATH MAY DEVIATE FROM A PATH
     * HEADING'/' STRAIGHT TOWARDS YOU (ATTACK) OR STRAIGHT AWAY FROM YO
     *U (RETREAT).'/' EG. 0 MEANS THEY WILL HEAD STRAIGHT FOR YOU WHEN T
     *HEY ATTACK'/5X,'OR STRAIGHT AWAY FROM YOU WHEN THE RETREAT.'/' EG.
     * 90 MEANS THEIR PATH MAY RANDOMLY DEVIATE OFF TO THE SIDE A MAXIMU
     *M'/5X,'OF 90 DEGREES FROM HEADING STAIGHT AT YOU OR STAIGHT AWAY F
     *ROM YOU')
      N=3
      READ(L,9,ERR=73)DISTP

CV SET CHOSEN ATTACK/RETEAT ANGLE (IDEVZ) TO INTEGER OF DISTP
      IDEVZ=IFIX(DISTP)

CV IF ANSWER IS ILLEGAL: REPEAT QUESTION (131)
      IF(IDEVZ.LT.0.OR.IDEVZ.GT.180)GO TO 131

CV ================
CV ASK ENGINE TYPE
CV ================

15    WRITE(L,16)
16    FORMAT(' TYPE OF ENGINES -- ENTER A NUMBER-'/
     *' ''1'' FOR IONIC DRIVE ENGINES'/
     *' ''2'' FOR WARP DRIVE ENGINES')
      N=4
      READ(L,9,ERR=73)DISTP

CV SET TYPE OF ENGINE TO INTEGER OF DISTP
      IDRIZ=IFIX(DISTP)

CV IF ANSWER IS ILLEGAL: REPEAT QUESTION (15)
      IF(IDRIZ.LT.1.OR.IDRIZ.GT.2)GO TO 15

CV =================
CV ASK MAXIMUM WARP
CV =================

21    WRITE(L,20)
20    FORMAT(' ENTER MAXIMUM WARP -- BETWEEN 2 AND 12')
      N=5
      READ(L,9,ERR=73)DISTP

CV SET MAXIMUM WARP TO INTEGER OF DISTP
      IMAXZ=IFIX(DISTP)

CV IF ANSWER IS ILLEGAL: REPEAT QUESTION (21)
      IF(IMAXZ.LT.2.OR.IMAXZ.GT.12)GO TO 21

CV =================================
CV ASK ION STORM / SPEED DEPENDENCY
CV =================================

      WRITE(L,25)
25    FORMAT(' DO YOU WANT THE KZINTIS'' SPEED TO BE UNAFFECTED BY ION S
     *TORMS?')
      N=6
      READ(L,11,ERR=73)NA
11    FORMAT(A1)

CV IF ANSWER = Y SET SPEED DEPENDENCY FLAG TO "NO" (1)
      IF(NA.EQ.'Y')IONNO=1

CV ==============
CV ASK ARMAMANET
CV ==============

30    WRITE(L,31)
31    FORMAT(' ARMAMENT -- ENTER A STRING OF DIGITS-'/
     *' ''1'' FOR INDUCTION BEAMS'/
     *' ''2'' FOR X-RAY LASER CANNON'/
     *' ''3'' FOR WEB (LIKE THOLIAN''S)'/
     *' ''4'' FOR DECOY IMAGES'/
     *' ''5'' FOR CLOAKING DEVICE'/
     *' AND/OR ''6'' FOR STASIS FIELD')
      READ(L,32,ERR=73)(IONK(I7),I7=1,6)
32    FORMAT(6I1)

CV LOOP OVER ARM TYPES I8 FOR 1 TO 6
      DO 33 I8=1,6

CV SET LOOPED ARM TYPE ACTIVE FLAG TO "0" (NOT ACTIVE)
      IARMZ(I8)=0

CV LOOP OVER CHOSEN ARM TYPES I7 FOR 1 TO 6
      DO 33 I7=1,6

CV IF LOOPED ARM TYPE = CHOSEN ARM TYPE:
CV SET ARM TYPE ACTIVE FLAG TO "1" (ACTIVE)
      IF(IONK(I7).EQ.I8)IARMZ(I8)=1

CV NEXT LOOPED ARM TYPE
33    CONTINUE

CV LOOP OVER CHOSEN ARM TYPES I8 FOR 1 TO 6
      DO 330 I8=1,6

CV SET IONK(CHOSEN ARM TYPE) TO "0" ?????
330   IONK(I8)=0

CV ================================
CV ASK FOR INDUCTION BEAM STRENGTH
CV ================================

      IF(IARMZ(1).EQ.0)GO TO 40
35    WRITE(L,34)
34    FORMAT(' ENTER INDUCTION BEAM STRENGTH IN PHASER UNITS')
      N=7
      READ(L,9,ERR=73)DISTP

CV IF INDUCTION BEAM STRENGTH >= 250: SKIP "TOO SMALL" MESSAGE (400)
      IF(DISTP.GE.250.)GO TO 400

      WRITE(L,401)
401   FORMAT(' NUMBER TOO SMALL- MINIMUM IS 250')

CV REPEAT QUESTION
      GO TO 35

CV SET INDUCTION BEAM STRENGTH TO INTEGER OF ANSWER/10
400   INDUZ=IFIX(DISTP)/10

CV ==============================
CV ASK FOR X-RAY CANNON STRENGTH
CV ==============================

40    IF(IARMZ(2).EQ.0)GO TO 70
71    WRITE(L,72)
72    FORMAT(' ENTER X-RAY CANNON STRENGTH IN PHASER UNITS')
      N=8
      READ(L,9,ERR=73)DISTP

CV IF X-RAY CANNON STRENGTH >= 250: SKIP "TOO SMALL" MESSAGE (700)
      IF(DISTP.GE.250.)GO TO 700

      WRITE(L,401)

CV REPEAT QUESTION
      GO TO 71

CV SET X-RAY CANNON STRENGTH TO INTEGER OF ANSWER/10
700   IXRYZ=IFIX(DISTP)/10

CV =====================
CV ASK FOR RANGE OF WEB
CV =====================

70    IF(IARMZ(3).EQ.0)GO TO 50
41    WRITE(L,42)
42    FORMAT(' ENTER RANGE OF WEB')
      N=9
      READ(L,9,ERR=73)DISTP

CV SET RANGE OF WEB TO INTEGER OF ANSWER
      IWEBZ=IFIX(DISTP)

CV IF RANGE OF WEB <= 0: REPEAT QUESTION (41)
      IF(IWEBZ.LE.0)GO TO 41

CV ==========================================
CV ASK FOR NUMBER OF FALSE IMAGES AND RADIUS
CV ==========================================

50    IF(IARMZ(4).EQ.0)GO TO 53

54    WRITE(L,55)
55    FORMAT(' ENTER NUMBER OF FALSE IMAGES TO BE GENERATED-
     * AT MOST 15')
      N=10
      READ(L,9,ERR=73)DISTP

CV SET NUMBER OF FALSE IMAGES TO INTEGER OF ANSWER
      IMAGZ=IFIX(DISTP)

CV IF NUMBER OF FALSE IMAGES < 0 OR > 15: REPEAT QUESTION (54)
      IF(IMAGZ.LT.0.OR.IMAGZ.GT.15)GO TO 54

58    WRITE(L,57)
57    FORMAT(' ENTER RADIUS WITHIN WHICH THE IMAGES ARE TO BE
     * GENERATED')
      N=11
      READ(L,9,ERR=73)DISTP

CV SET RADIUS TO INTEGER OF ANSWER
      IMAGRZ=IFIX(DISTP)

CV IF RADIUS < 1 OR > 50: REPEAT QUESTION (58)
      IF(IMAGRZ.LT.1.OR.IMAGRZ.GT.50)GO TO 58

CV ===========================
CV ASK FOR RANGE OF DETECTION
CV ===========================

53    IF(IARMZ(5).EQ.0)GO TO 60
51    WRITE(L,52)
52    FORMAT(' ENTER RANGE AT WHICH ENTERPRISE CAN DETECT THE KZINTI SH
     *IP'/' THROUGH THE CLOAKING DEVICE')
      N=12
      READ(L,9,ERR=73)DISTP

CV SET CLOAKING DEVICE RANGE TO INTEGER OF ANSWER
      ICLOZ=IFIX(DISTP)

CV IF CLOAKING DEVICE RANGE <= 0: REPEAT QUESTION
      IF(ICLOZ.LE.0)GO TO 51

CV ===========================================
CV ASK FOR RANGE AND STRENGTH OF STASIS FIELD
CV ===========================================

60    IF(IARMZ(6).EQ.0)GO TO 29

61    WRITE(L,62)
62    FORMAT(' ENTER RANGE OF STASIS FIELD')
      N=13
      READ(L,9,ERR=73)DISTP

CV SET RANGE OF STASIS FIELD TO INTEGER OF ANSWER
      IABSZ=IFIX(DISTP)

CV IF RANGE OF STASIS FIELD <= 0: REPEAT QUESTION (61)
      IF(IABSZ.LE.0)GO TO 61

64    WRITE(L,63)
63    FORMAT( ' ENTER STRENGTH OF STASIS FIELD IN EQUIVALENT PHASER UNI
     *TS PER TURN')
      N=14
      READ(L,9,ERR=73)DISTP

CV SET STRENGTH OF STASIS FIELD TO INTEGER OF ANSWER
      ISTAZ=IFIX(DISTP)

CV IF STRENGTH OF STASIS FIELD < 1: REPEAT QUESTION (64)
      IF(ISTAZ.LT.1)GO TO 64

CV =====================
CV ASK NEUTRAL DISTANCE
CV =====================

CV SET NEUTRAL DISTANCE TO 100
29    KILLZ=100
      WRITE(L,27)
27    FORMAT(' IF YOU WISH THE KZINTI TO BE NEUTRAL UNTIL FIRED AT'/' O
     *R UNTIL YOU COME WITHIN A CERTAIN RANGE, ENTER THE RANGE AT WHICH'
     */' THEY BECOME HOSTILE.'/' IF NOT, TYPE ''100''.')
      N=15
      READ(L,9,ERR=73)DISTP

CV SET NEUTRAL DISTANCE TO INTEGER OF ANSWER
      KILLZ=IFIX(DISTP)

CV IF NEUTRAL DISTANCE <= 0 OR > 100: REPEAT QUESTION
      IF(KILLZ.LE.0.OR.KILLZ.GT.100)GO TO 29

CV ========================================
CV SET AUTOMATIC MINIMUM STRENGTH SETTINGS
CV ========================================

CV IF EITHER INDUCTION BEAMS OR X-RAY LASER CANNON ACTIVE:
CV SKIP AUTOMATIC MINIMUM STRENGTH SETTINGS (28)
280   IF(IARMZ(1).EQ.1.OR.IARMZ(2).EQ.1)GO TO 28

CV ELSE

CV SET INDUCTION BEAM ACTIVE
      IARMZ(1)=1

CV SET X-RAY LASER CANNON ACTIVE
      IARMZ(2)=1

CV SET INDUCTION BEAM STRENGTH TO 90 (EQUAL TO 900 PHASER UNITS)
      INDUZ=90

CV SET X-RAY LASER CANNON STRENGTH TO 90 (EQUAL TO 900 PHASER UNITS)
      IXRYZ=90

28    RETURN

CV =======================================================
CV MANAGE FORMALLY ILLEGAL ANSWERS USING GOTO POINTER "N"
CV =======================================================

73    CALL ILLDAT
      GO TO (7,10,131,15,21,30,35,71,41,54,58,51,61,64,280),N
      END
