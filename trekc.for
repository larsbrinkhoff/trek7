C=====================================================
C
C TREK7   MODULE C
C
C M-O SHIP OPERATIONS
C
C SULU    DUNSEL   LAUREL  HARDY   SPOCK   SAREK
C MUDD    HARPO    TPRING  CHICO   ZEPPO   TANRU
C LARRY   CURLY    MOE
C
C=====================================================

C       -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C        SSSS  U   U    L      U   U
C       S      U   U    L      U   U
C        SSS   U   U    L      U   U
C           S  U   U    L      U   U
C       SSSS    UUU     LLLLL   UUU

CV MOVE M-O SHIPS TO INTERMEDIATE / TARGET POSITION
CV INCLUDING ALL POSSIBLE OBSTACLES AND ADVERSITIES
CV
CV ==========================
CV INPUT TRANSFER PARAMETERS
CV ==========================
CV J      - NOT USED ALTHOUGH PARAMETER TO SULU ???
CV IT     - INDEX OF M-O SHIP TO MOVE
CV IENTR  - CURRENT M-O SHIP ROW
CV IENTC  - CURRENT M-O SHIP COL
CV ITROW  - TARGET ROW
CV ITCOL  - TARGET COL
CV
CV ===================
CV INTERNAL VARIABLES
CV ===================
CV Q      - ACTUAL WARP SPEED
CV
CV ==================
CV OUTPUT PARAMETERS
CV
CV ISTAT  - ???
CV
CV ======
CV STEPS (NOT NECESSARILY EXECUTED IN THAT ORDER!)
CV ======
CV  1. DAMAGES DISTINCTION
CV  2. POTENTIONAL WEB TRAPPING/ESCAPING
CV  3. POTENTIONAL DAMAGES BY WEB (LAUREL)
CV  4. SPEED MANAGEMENT & POTENTIAL VEERING
CV  5. CALCULATE RANDOM VEERING TARGET
CV  6. ARRIVED AT TARGET / INTERMEDIATE POSITION
CV  7. POTENTIONAL DEFLECTOR BEAM / TRACTOR BEAM (DUNSEL, HORTA)
CV  8. POTENTIONAL COLLISION WITH C-O SHIP
CV  9. POTENTIAL M-O SHIP / ANOTHER M-O SHIP RAMMING
CV 10. POTENTIAL COURSE BLOCKAGE BY A STAR (HORTA)
CV 11. MOVE M-O SHIP TO INTERMEDIATE / TARGET POSITION
CV 12. POTENTIAL DILITHIUM SENSING
CV 13. POTENTIAL DILITHIUM ACQUISITION AND PROCESSING
CV 14. POTENTIAL TARGET POSITION IN ION STORM
CV 15. POTENTIAL COLLISION OF OBJECT AND MINE
CV 16. POTENTIAL COLLISION OF OBJECT AND EAGLE
CV 17. DAMAGE CALCULATION FROM MINE OR EAGLE COLLISION (GRUP1)
CV FINISH
CV
      LOGICAL FUNCTION SULU(J)
      LOGICAL CYRANO
C
C
        INTEGER ITZRO,ITVL1
        REAL RTZRO,RTVL1
C INSERTED TO TAKE CARE OF CALL LIST PROBLEMS
C***
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /F/WARP(4),ITROW(4),ITCOL(4)
      COMMON /I/IDNK(8),ISPOK(8),ION(4),ISPOT(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /O/MA(4,33),K(4,14),NOMAP(4),MANUM(4),HIVEL(4,2),ITEMP(4),
     *NOMOV(4)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)
      COMMON /V/IWEB(2),IWEBZ,INVIS(4)
      COMMON /TOM/ITOM

CV SET "REACHED TARGET COORDINATES" TO FALSE
      SULU=.FALSE.

CV ???
      ISTAT=0

CV IF REACHED TARGET COORDINATES (ITCOL/ITROW) YES:
CV GOTO 1002 (VEERING DECISION)
      IF(ITCOL(IT).EQ.IENTC(IT).AND.ITROW(IT).EQ.IENTR(IT))GO TO 1002

CV ====================
CV DAMAGES DISTINCTION
CV ====================

CV IF NOT ANY OF THE ENGINE NACELLES DESTROYED: CV LOOP OVER WEBS (110)
      IF(MA(IT,29).NE.9.OR.MA(IT,30).NE.9)GO TO 110

CV IF IMPULSE ENGINE DESTROYED: MESSAGE, WARP 0, RETURN
      IF(MA(IT,28).EQ.9)GO TO 111

CV IF WARP < 2: CV LOOP OVER WEBS (110)
      IF(WARP(IT).LT.2.0)GO TO 110

CV ELSE (ANY OF THE ENGINE NACELLES DESTROYED)

CV SET WARP TO 1.5
      WARP(IT)=1.5
      WRITE(L,113)
113   FORMAT(' WARP ENGINE NACELLES ARE DESTROYED AND THUS WARP GREATER
     * THAN 1 IS IMPOSSIBLE')
      GO TO 110
111   WRITE(L,112)
112   FORMAT(' ALL ENGINES ARE DESTROYED - MOVEMENT IMPOSSIBLE')
      WARP(IT)=0.0
      RETURN

CV ==================================
CV POTENTIONAL WEB TRAPPING/ESCAPING
CV ==================================

CV LOOP OVER WEBS (J) FOR 1 TO 2
110   DO 1000 J=1,2

CV IF WEB NOT FOR CURRENT SHIP: NO ESCAPE, NEXT WEB
      IF(IWEB(J).NE.IT)GO TO 1000

CV IF WARP < 10: NO ESCAPE, NEXT WEB
      IF(WARP(IT).LT.10.)GO TO 1000

CV CALCULATE WEB ESCAPE FACTOR IV (1/6 PROBABILITY)
      CALL RANDO(IV,1,7)

CV IF WEB ESCAPE FACTOR IV > 1: NO ESCAPE, NEXT WEB
      IF(IV.GT.1)GO TO 1000

CV ELSE (IV=1) WEB ESCAPE SUCCESSFUL!
      IWEB(J)=0

      WRITE(L,1001)(INAME(IQ0,IT),IQ0=1,3)
1001  FORMAT(' THE ',3A4,' HAS BROKEN FREE OF THE WEB')

CV NEXT WEB
1000  CONTINUE

CV IF BOTH WEBS NOT FOR CURRENT SHIP: 1004
      IF(IWEB(1).NE.IT.AND.IWEB(2).NE.IT)GO TO 1004

CV WRITE TRAPPING MESSAGE
      WRITE(L,1003)(INAME(IQ0,IT),IQ0=1,3)
1003  FORMAT(' THE ',3A4,' IS TRAPPED IN A WEB')
      RETURN

CV ===========================
CV POTENTIONAL DAMAGES BY WEB
CV ===========================

CV LOOP OVER WEBS (J) FOR 1 TO 2
1004  DO 100 J=1,2

CV CALCULATE DECK DAMAGE DEPENDENT NACELLE DAMAGE
      IF(MA(IT,J+30).EQ.0.OR.MA(IT,J+28).EQ.9)GO TO 100
      IF(MA(IT,J+30).EQ.9.AND.WARP(IT).GT.1.)GO TO 109
      IF(HIVEL(IT,J).GE.WARP(IT))GO TO 100
      I7=10*(WARP(IT)-HIVEL(IT,J))
      IF(I7.GT.0)I7=I7+10

CV CALCULATE DAMAGE FACTOR IV (1-100)
      CALL RANDO(IV,1,100)

CV IF DAMAGE FACTOR IV > I7: SKIP NACELLE DAMAGE
      IF(IV.GT.I7)GO TO 100

CV NACELLE DAMAGE
109   CALL LAUREL(J)

CV NEXT WEB
100   CONTINUE

CV =====================================
CV SPEED MANAGEMENT & POTENTIAL VEERING
CV =====================================

CV IF ACTUAL SPEED > AVAILABLE SPEED
205   IF(WARP(IT).GT.TWARP(IT))WARP(IT)=TWARP(IT)

CV IF WARP <= 1: NO VEERING
1002  IF(WARP(IT).LE.1.)GO TO 10020

CV CALCULATE RANDOM VEERING FACTOR I7 (FROM 1 TO 100)
CV HIGHER VEERING FACTOR: LESS PROBABILITY OF VEERING
CV IF VEERING FACTOR > 33: NO VEERING REGARDLESS OF DAMAGE
      CALL RANDO(I7,1,100)

CV IF VEERING FACTOR I7 > (DAMAGE FACTOR OF RELEVANT DECKS (#20)+1)*3:
CV NO VEERING
CV IF NO DAMAGE: VEERING FACTOR I7 > 3: NO VEERING
      IF(I7.GT.(MA(IT,20)+1)*3)GO TO 10020

CV J = INTEGER(AVAILABLE WARP POWER)
      J=IFIX(TWARP(IT))

CV IF AVAILABLE WARP POWER < 1: RETURN
      IF(J.EQ.0)RETURN

      WRITE(L,200)
200   FORMAT(' SHORT-CIRCUIT IN WARP DRIVE ENGINEERING ROOM-'/' OUR SHIP
     * HAS LOST STEERING CONTROL AND IS VEERING OFF COURSE')

CV ================================
CV CALCULATE RANDOM VEERING TARGET
CV ================================

CV CALCULATE RANDOM VEERING FACTOR IV IN RANGE 0 TO J
201   CALL RANDO(IV,0,J)

CV CALCULATE RANDOM VEERING ROW TARGET
CV IN RANGE ROW +/- RANDOM VEERING FACTOR IV
      CALL RANDO(I7,IENTR(IT)-IV,IENTR(IT)+IV)

CV CALCULATE RANDOM VEERING COL TARGET
CV IN RANGE COL +/- RANDOM VEERING FACTOR IV
      CALL RANDO(I8,IENTC(IT)-IV,IENTC(IT)+IV)

CV I7: ROW POSITION
CV I8: COLUMN POSITION

CV IF RANDOM VEERING TARGET OUT OF 60X60 AREA: GOTO 201 (RECALCULATE TARGET)
      IF(CYRANO(I7,I8))GO TO 201

CV GET NA (WHAT IS AT CALCULATED RANDOM VEERING TARGET)
      NA=MAP(I7,I8)

CV IF AT RANDOM VEERING TARGET TARGET THERE IS NEITHER BLANK (EMPTY)
CV NOR ION STORM (THEN THERE IS ANOTHER OBJECT):
CV RECALCULATE RANDOM VEERING TARGET
      IF(NA.NE.IBLK.AND.NA.NE.III)GO TO 201

CV ???
      J=0

CV MOVE M-O SHIP TO VEERING TARGET
      GO TO 5327

CV ==========================================
CV ARRIVED AT TARGET / INTERMEDIATE POSITION
CV ==========================================

CV IF TARGET COORDINATES (ITCOL/ITROW) REACHED:
CV WRITE "... REACHED TARGET COORDINATES" MESSAGE (600)
10020 IF(ITCOL(IT).EQ.IENTC(IT).AND.ITROW(IT).EQ.IENTR(IT))GO TO 600

CV ELSE: ARRIVED AT INTERMEDIATE POSITION

CV SET LOCAL VARIABLE Q TO ACTUAL WARP SPEED
      Q=WARP(IT)

CV ==========================================
CV POTENTIONAL DEFLECTOR BEAM / TRACTOR BEAM
CV ==========================================

CV CALCULATE BEARING ANGLE
      BERNG=ANG(ITROW(IT)-IENTR(IT),ITCOL(IT)-IENTC(IT))

CV LOOP OVER M-O SHIPS I7 FOR 1 TO 4
      DO 224 I7=1,4

CV IF SHIP IS NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).NE.1)GO TO 224

CV IF SHIP IS ACTUAL SHIP: NEXT M-O SHIP
      IF(I7.EQ.IT)GO TO 224

CV "CAUGHT IN A DEFLECTOR BEAM" FACTOR ???
      MIN=0

CV CALCULATE EFFECT OF DEFLECT AND YANK COMMANDS (IF A TARGET IS HIT)
      CALL DUNSEL(IENTR(IT),IENTC(IT),Q,BERNG)

CV MEANING OF MIN VALUES RETURNED BY DUNSEL:
CV MIN=0: NEITHER PUSH NOR PULL
CV MIN=1: PULL
CV MIN=2: PUSH
CV MIN=3: PULL&PUSH

CV IF MIN < 2: SKIP MESSAGE "CAUGHT IN A DEFLECTOR BEAM"
      IF(MIN.LT.2)GO TO 222

CV DECREASE MIN BY 2
      MIN=MIN-2

CV WRITE MESSAGE "CAUGHT IN A DEFLECTOR BEAM"
      WRITE(L,223)(INAME(IQ0,I7),IQ0=1,3)
223   FORMAT(' THE ',3A4,' HAS US CAUGHT IN A DEFLECTOR BEAM')

CV IF MIN = 0: SKIP MESSAGE "CAUGHT IN A TRACTOR BEAM", NEXT M-O SHIP
222   IF(MIN.EQ.0)GO TO 224
      WRITE(L,225)(INAME(IQ0,I7),IQ0=1,3)
225   FORMAT(' THE ',3A4,' HAS US CAUGHT IN A TRACTOR BEAM')

CV NEXT M-O SHIP
224   CONTINUE

CV IF ACTUAL WARP SPEED < 1: RETURN
CV (WARP < 1 DOES NOT MOVE THE SHIP, NOT EVEN IF GO/MOVE SEVERAL TIMES!)
      IF(Q.LT.1.)RETURN

CV ELSE (ACTUAL WARP SPEED >= 1):
      N=1
      IV=IT
      IGNORE=0
        ITZRO=0
        RTZRO=0.0
        ITVL1=1

CV CALCULATE WHERE THE COURSE LEADS TO AND WHAT IS AT THAT POSITION
770   CALL HORTA(IENTR(IT),IENTC(IT),ITROW(IT),ITCOL(IT),Q,
     *RTZRO,ITVL1,RTZRO,IGNORE,RTZRO,ITZRO)

CV WHY SET "J=0" HERE?
CV (SEE DO LOOP "DO 1390 J=1,4" BELOW)
      J=0

CV ====================================
CV POTENTIONAL COLLISION WITH C-O SHIP
CV ====================================

CV IF NEITHER M-O SHIP NOR C-O SHIP:
CV POTENTIAL COURSE BLOCKAGE BY A STAR (532)
      IF(MIN.GT.12)GO TO 532

CV SET DESTRUCTION FLAG FOR M-O SHIP INDEX IT
      DFLCT(IT)=-1.

CV IF OBJECT IS M-O SHIP: POTENTIAL M-O SHIP /
CV ANOTHER M-O SHIP RAMMING (166)
      IF(MIN.LT.5)GO TO 166

CV RESET OBJECT INDEX MIN TO C-O SHIP INDEX
      MIN=MIN-4

CV LOOP OVER M-O SHIPS J FOR 1 TO 4
      DO 1390 J=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(J).EQ.0)GO TO 1390

CV SET TERMINAL INDEX TO J+4
      N=J+4

CV WRITE COLLISON MESSAGE
      WRITE(N,235)(INAME(IQ0,IT),IQ0=1,3),(IENM2(IV,MIN),IV=1,4)
235   FORMAT(' THE ', 3A4, ' HAS COLLIDED WITH THE ',4A4/' WHICH HAS
     * EXPLODED')

CV NEXT M-O SHIP
1390  CONTINUE

CV SET DESCTRUCTION FLAG FOR COLLIDED C-O SHIP INDEX MIN
CV (MIN NOW: C-O SHIP INDEX)
      DFLCK(MIN)=-1.

CV
      GO TO 343

CV ==============================================
CV POTENTIAL M-O SHIP / ANOTHER M-O SHIP RAMMING
CV ==============================================

CV LOOP OVER M-O SHIPS J FOR 1 TO 4
166   DO 167 J=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(J).EQ.0)GO TO 167

CV SET TERMINAL INDEX TO J+4
      N=J+4

CV WRITE RAMMING MESSAGE
      WRITE(N,168)(INAME(IQ0,IT),IQ0=1,3),(INAME(IQ0,MIN),IQ0=1,3)
168   FORMAT(' THE ',3A4,' HAS RAMMED THE ',3A4)

CV NEXT M-O SHIP
167   CONTINUE

CV SET DESCTRUCTION FLAG FOR OTHER M-O SHIP (INDEX MIN)
      DFLCT(MIN)=-1.

CV DELETE OBJECT IN MAP AT I7/I8
343   MAP(I7,I8)=IBLK

CV DELETE OBJECT IN MAP AT IENTR(IT)/IENTR(IT)
      MAP(IENTR(IT),IENTC(IT))=IBLK

CV IF ION STORM FLAG SET: RESTORE ION STORM FLAG AT IENTR(IT)/IENTR(IT)
      IF(ISPOT(IT).EQ.1)MAP(IENTR(IT),IENTC(IT))=III

      RETURN

CV ====================================
CV POTENTIAL COURSE BLOCKAGE BY A STAR
CV ====================================

CV IF OBJECT IS NOT A STAR: 5330
532   IF(MIN.NE.13)GO TO 5330

CV ELSE: COURSE BLOCKED BY A STAR
5320  WRITE(L,535)
535   FORMAT(' OUR COURSE IS BLOCKED BY A STAR')
      IV1=I7
      IVV=I8
      RTVL1=1.5
      ITVL1=1
      ITZRO=0
      RTZRO=0.0

CV CALCULATE INTERMEDIATE POSITION IN FRONT OF BLOCKING STAR
      CALL HORTA(IV1,IVV,IENTR(IT),IENTC(IT),RTVL1,RTZRO,ITZRO,
     *RTZRO,ITVL1,RTZRO,ITZRO)

CV ================================================
CV MOVE M-O SHIP TO INTERMEDIATE / TARGET POSITION
CV ================================================

CV SET MAP POSITION TO EMPTY (BLANK)
5327  MAP(IENTR(IT),IENTC(IT))=IBLK

CV IF THERE WAS AN ION STORM IN THIS POSITION: RESTORE IT
      IF(ISPOT(IT).EQ.1)MAP(IENTR(IT),IENTC(IT))=III

      ISPOT(IT)=0
      IENTR(IT)=I7
      IENTC(IT)=I8

CV IF MOVEBILITY OF SHIP = FULL: SET MOVEBILITY TO REDUCED
      IF(IGO(IT).EQ.2)IGO(IT)=1


      IF(MAP(IENTR(IT),IENTC(IT)).EQ.III)ISPOT(IT)=1

      IF(J.EQ.1)GO TO 770

CV PUT M-O SHIP SYMBOL INTO NEW POSITION IENTR/IENTC
      MAP(IENTR(IT),IENTC(IT))=IEE(IT)

CV IF NOT AT TARGET COORDINATES: SKIP "... REACHED TARGET COORDINATES" MESSAGE
      IF(IENTR(IT).NE.ITROW(IT).OR.IENTC(IT).NE.ITCOL(IT))GO TO 550

CV WRITE "... REACHED TARGET COORDINATES" MESSAGE
600   WRITE(L,601)
601   FORMAT(' WE''VE REACHED TARGET COORDINATES')

CV
      SULU=.TRUE.

CV ============================
CV POTENTIAL DILITHIUM SENSING
CV ============================

CV LOOP OVER DILITHIUM STARS I7 FOR 1 TO 5
550   DO 552 I7=1,5

CV IF  DILITHIUM STAR I7 NOT IN GAME: NEXT DILITHIUM STAR (552)
      IF(LI2R(I7).EQ.0)GO TO 552

CV IF DILITHIUM STAR CLAIMED BY M-O SHIP : NEXT DILITHIUM STAR (552)
      IF(LI2(IT).EQ.I7)GO TO 552

CV IF DILITHIUM STAR MORE THAN 5 ROWS AWAY: SKIP DETECTION MESSAGE,
CV NEXT DILITHIUM STAR (552)
      IF(IABS(IENTR(IT)-LI2R(I7)).GT.5)GO TO 552

CV IF DILITHIUM STAR MORE THAN 5 COLS AWAY: SKIP DETECTION MESSAGE,
CV NEXT DILITHIUM STAR (552)
      IF(IABS(IENTC(IT)-LI2C(I7)).GT.5)GO TO 552

CV CALCULATE RANDOM PLANET NUMBER I8
      CALL RANDO(I8,1,12)

      WRITE(L,553)I8,LI2C(I7),LI2R(I7)
553   FORMAT(' SENSORS DETECT A DEPOSIT OF DILITHIUM CRYSTALS ON PLANET
     * ',I2/' OF SYSTEM (',I2,',',I2,'). ACQUISITION OPERATIONS WILL COM
     *MENCE'/' IF WE CLOSE TO WITHIN 1 UNIT OF THE SYSTEM.')

CV    SET M-O SHIP IT CLAIM FOR DILITHIUM STAR I7
      LI2(IT)=I7

CV GO TO POTENTIAL DILITHIUM ACQUISITION AND PROCESSING
      GO TO 430

CV NEXT DILITHIUM STAR
552   CONTINUE

CV ===============================================
CV POTENTIAL DILITHIUM ACQUISITION AND PROCESSING
CV ===============================================

CV LOOP OVER DILITHIUM STARS I7 FOR 1 TO 5
430   DO 5520 I7=1,5

CV IF DILITHIUM STAR I7 NOT IN GAME: NEXT DILITHIUM STAR (5520)
      IF(LI2R(I7).EQ.0)GO TO 5520

CV IF DILITHIUM STAR MORE THAN 1 ROW AWAY: NEXT DILITHIUM STAR
      IF(IABS(IENTR(IT)-LI2R(I7)).GT.1)GO TO 5520

CV IF DILITHIUM STAR MORE THAN 1 COL AWAY: NEXT DILITHIUM STAR
      IF(IABS(IENTC(IT)-LI2C(I7)).GT.1)GO TO 5520

CV CALCULATE RANDOM DILITHIUM PHASER POWER J
      CALL RANDO(J,3500,6500)

CV WRITE MESSAGE "ACQUISITION AND PROCESSING OF DILITHIUM CRYSTALS..."
      WRITE(L,551)LI2C(I7),LI2R(I7),J
551   FORMAT(' ACQUISITION AND PROCESSING OF DILITHIUM CRYSTALS FROM SYS
     *TEM (',I2,',',I2,') PROCEEDING. '/' PHASER ENERGY INCREASED BY',I5,
     *' UNITS. ACQUISITION TERMINATED.')

CV INCREASE AVAILABLE PHASER POWER BY DILITHIUM PHASER POWER
      PHASR(IT)=PHASR(IT)+J

CV SET DILITHIUM STAR OUT OF GAME (THE STAR ITSELF REMAINS UNTOUCHED)
      LI2R(I7)=0
      LI2C(I7)=0

CV SET DILITHIUM STAR CLAIM OF M-O SHIP IT TO "0"
5501  LI2(IT)=0

CV NEXT DILITHIUM STAR
5520  CONTINUE

5321  RETURN

CV =======================================
CV POTENTIAL TARGET POSITION IN ION STORM
CV =======================================

5322  IF(MIN.NE.19)GO TO 5327
      ION(IT)=1
      IGNORE=1
      Q=(Q-DISTP)/2.
      J=1
      IF(I7.EQ.ITROW(IT).AND.I8.EQ.ITCOL(IT))J=0
      GO TO 5327

CV =======================================
CV POTENTIAL COLLISION OF OBJECT AND MINE
CV =======================================

5323  IF(MIN.GT.18)GO TO 5322
      DO 228 IV=1,4
      IF(ICHOE(IV).EQ.0)GO TO 228
      N=IV+4
      WRITE(N,16)(INAME(IQ0,IT),IQ0=1,3),
     *(INAME(IQ0,MIN-14),IQ0=1,3),I8,I7
16    FORMAT(1X,3A4,' COLLIDED WITH ',3A4,' MINE AT (',I2,',',I2,')')
228   CONTINUE

CV GO TO DAMAGE CALCULATION FROM MINE OR EAGLE COLLISION
      GO TO 227

CV ========================================
CV POTENTIAL COLLISION OF OBJECT AND EAGLE
CV ========================================

CV IF OBJECT IS NOT AN EAGLE: 5323
5330  IF(MIN.NE.14)GO TO 5323

CV LOOP OVER EAGLES IV FOR 1 TO LAUNCH
      DO 5331 IV=1,LAUNCH

CV IF TARGET POSITION = EAGLE POSITION: 5332
      IF(I7.EQ.IGLER(IV).AND.I8.EQ.IGLEC(IV))GO TO 5332

CV NEXT EAGLE
5331  CONTINUE

5332  DO 226 N=1,4
      IF(ICHOE(N).EQ.0)GO TO 226
      IVV=N+4
      WRITE(IVV,5333)(INAME(IQ0,IT),IQ0=1,3),IV,I8,I7,IV,
     *(INAME(IQ0,IT),IQ0=1,3)
5333  FORMAT(1X,3A4,' COLLIDED WITH EAGLE',I3,' AT (',I2,',',I2,')'/' EA
     *GLE',I3,' DESTROYED'/' DAMAGE REPORT TO THE ',3A4)
226   CONTINUE

      CALL BOOM(IV)

CV ================================================
CV DAMAGE CALCULATION FROM MINE OR EAGLE COLLISION
CV ================================================

CV CALCULATE RANDOM DAMAGE FACTOR IV (1-100)
227   CALL RANDO(IV,1,100)

CV CALCULATE DAMAGE NUMBER IVV FROM DEFLECTOR STRENGTH AND DAMAGE FACTOR IV
      IVV=ALOG((101.-DFLCT(IT))*IV+10.)/0.700619195-1.8185

CV DAMAGE MANAGEMENT FOR M-O SHIPS
      CALL GRUP1(IVV,IT)

      GO TO 5327

      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR     T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C - DUNSEL -
C
CV RESULT OF PUSH (DEFLECT) AND PULL (YANK) COMMANDS

      SUBROUTINE DUNSEL(J1,J2,Q1,Q2)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /P/IPULL(4),IPUSH(4),PULL(4),PUSH(4),IPULLR(4),IPULLC(4),
     *IPUSHR(4),IPUSHC(4)

CV IF PULL = 0: SKIP PULL CALCULATIONS
      IF(IPULL(I7).EQ.0)GO TO 1
      IF(IPULLC(I7).NE.J2.OR.IPULLR(I7).NE.J1)GO TO 1
      IPULL(I7)=0
      B=ANG(IENTR(I7)-J1,IENTC(I7)-J2)
      IV=ABS(B-Q2)
      IF(IV.LT.90.GT.IV.GT.270)GO TO 1
      MIN=1
      Q1=Q1/2.0**(PULL(I7)*2.0)

CV IF PUSH = 0: SKIP PUSH CALCULATIONS (IE. RETURN)
1     IF(IPUSH(I7).EQ.0)GO TO 2
      IF(IPUSHC(I7).NE.J2.OR.IPUSHR(I7).NE.J1)GO TO 2
      IPUSH(I7)=0
      B=ANG(J1-IENTR(I7),J2-IENTC(I7))
      IV=ABS(B-Q2)
      IF(IV.LT.90.OR.IV.GT.270)GO TO 2
      Q1=Q1/2.0**(PUSH(I7)/5.)
      MIN=MIN+2

2     RETURN
      END


C  -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C  - LAUREL -
C
CV NACELLE DAMAGE

      SUBROUTINE LAUREL(J)

        INTEGER ITZRO,ITVL1
        REAL RTZRO,RTVL1
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /F/WARP(4),ITROW(4),ITCOL(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /O/MA(4,33),K(4,14),NOMAP(4),MANUM(4),HIVEL(4,2),ITEMP(4),
     *NOMOV(4)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)



CV WRITE NUMBER OF DAMAGED NACELLE
109   WRITE(L,102)J
102   FORMAT(' NACELLE',I2,' HAS RIPPED FREE OF THE SECONDARY HULL')

CV IF MOVEBILITY OF SHIP = FULL: SET MOVEBILITY TO REDUCED
      IF(IGO(IT).EQ.2)IGO(IT)=1

CV IF DAMAGE OF DECK J+28 > 0: REDUCE NUMBER OF DAMAGED DECKS BY 1 (?)
      IF(MA(IT,J+28).GT.0)MANUM(IT)=MANUM(IT)-1

CV RAISE NUMBER OF DAMAGED DECKS BY 1
      MANUM(IT)=MANUM(IT)+1

CV SET DAMAGE FACTOR TO MAXIMUM FOR RELEVANT TWO DECKS
      MA(IT,J+28)=9
      MA(IT,J+30)=9

CV CALCULATE REDUCED AVAILABLE WARP POWER TWARP
      TWARP(IT)=TWARP(IT)-FLOAT(MA(IT,J+28)-K(IT,J+3))/2.

CV AVAILABLE WARP POWER TWARP CANNOT SINK BELOW ZERO
      IF(TWARP(IT).LT.0)TWARP(IT)=0.

CV CALCULATE SPECIAL DAMAGE FACTOR "K"
      K(IT,J+3)=MA(IT,J+28)
      N=2
      ITZRO=0
      RTZRO=0.
      RTVL1=85.0
      ITVL1=1
      IV=IT

CV
      CALL HORTA(IENTR(IT),IENTC(IT),ITROW(IT),ITCOL(IT),
     *RTVL1,RTZRO,ITZRO,RTZRO,ITVL1,RTZRO,ITZRO)

CV =========
CV M-O SHIP
CV =========

CV IF NOT AN M-O SHIP (MIN > 4): SKIP TO 202
      IF(MIN.GT.4)GO TO 202

CV LOOP OVER M-O SHIPS FOR 1 TO 4
      DO 203 N=1,4

CV IF M-O SHIP NOT IN GAME: SKIP "NACELLE HIT ..." MESSAGE, NEXT SHIP
      IF(ICHOE(N).EQ.0)GO TO 203

CV CALCULATE TERMINAL NUMBER IV
      IV=N+4

CV WRITE "NACELLE HIT ..." MESSAGE
      WRITE(IV,204)(INAME(IQ0,IT),IQ0=1,3),J,(INAME(IQ0,MIN),IQ0=1,3)
204   FORMAT(1X,3A4,' NACELLE',I2,' HIT THE ',3A4)

CV NEXT M-O SHIP
203   CONTINUE

CV DAMAGE MANAGEMENT FOR MANUALLY OPERATED SHIPS
      CALL GRUP1(10,MIN)

CV MESSAGE "DAMAGE FROM SECONDARY EXPLOSIONS"
      CALL HARDY

CV DAMAGE MANAGEMENT FOR MANUALLY OPERATED SHIPS
      CALL GRUP1(10,MIN)

CV RETURN
      GO TO 205

CV =========
CV C-O SHIP
CV =========

CV IF NEITHER M-O NOR C-O SHIP: 206
202   IF(MIN.GT.12)GO TO 206

CV ELSE: C-O SHIP
      MIN=MIN-4

CV LOOP OVER M-O SHIPS N FOR 1 TO 4
      DO 207 N=1,4

CV IF SHIP NOT IN GAME: SKIP "NACELLE ... HIT" MESSAGE, NEXT SHIP
      IF(ICHOE(N).EQ.0)GO TO 207

CV CALCULATE TERMINAL NUMBER IV
      IV=N+4

CV WRITE "NACELLE ... HIT" MESSAGE
      WRITE(IV,208)(INAME(IQ0,IT),IQ0=1,3),J,(IENM2(IVV,MIN),IVV=1,4)
208   FORMAT(1X,3A4,' NACELLE',I2,' HIT ',4A4)

CV NEXT C-O SHIP
207   CONTINUE

CV DAMAGE MANAGEMENT FOR C-O SHIPS
      CALL GRUP3(10,MIN)

CV MESSAGE "DAMAGE FROM SECONDARY EXPLOSIONS"
      CALL HARDY

CV DAMAGE MANAGEMENT FOR C-O SHIPS
      CALL GRUP3(10,MIN)

CV RETURN
      GO TO 205

CV ==============
CV STAR/STARBASE
CV ==============

CV IF MIN <> 13: SKIP TO 209
206   IF(MIN.NE.13)GO TO 209

CV LOOP OVER STARBASES MI FOR 1 TO 2
      DO 210 MI=1,2

CV IF POSITION I7,I8 NOT POSITION OF STARBASE:
CV SKIP OVER DAMAGE MANAGEMENT FOR STARBASES
      IF(I7.NE.IBASR(MI))GO TO 210
      IF(I8.NE.IBASC(MI))GO TO 210

CV LOOP OVER M-O SHIPS N FOR 1 TO 4
      DO 211 N=1,4

CV IF SHIP NOT IN GAME: SKIP "NACELLE ... HIT ... STARBASE" MESSAGE, NEXT SHIP
      IF(ICHOE(N).EQ.0)GO TO 211

CV CALCULATE TERMINAL NUMBER IV
      IV=N+4

CV WRITE "NACELLE ... HIT ... STARBASE" MESSAGE
      WRITE(IV,212)(INAME(IQ0,IT),IQ0=1,3),J,(ISIDE(IQ0,MI),IQ0=1,3),
     *IBASE(MI)
212   FORMAT(1X,3A4,' NACELLE',I2,' HIT ',3A4,' STARBASE',I3)

CV NEXT STARBASE
211   CONTINUE

CV DAMAGE MANAGEMENT FOR STARBASES
      CALL GRUP2(10,MI)

CV MESSAGE "DAMAGE FROM SECONDARY EXPLOSIONS"
      CALL HARDY

CV DAMAGE MANAGEMENT FOR STARBASES
      CALL GRUP2(10,MI)

CV RETURN
      GO TO 205

CV NEXT STARBASE
210   CONTINUE

CV LOOP OVER M-O SHIPS N FOR 1 TO 4
      DO 213 N=1,4

CV IF SHIP NOT IN GAME: SKIP "NACELLE ... HIT" MESSAGE, NEXT SHIP
      IF(ICHOE(N).EQ.0)GO TO 213

CV CALCULATE TERMINAL NUMBER IV
      IV=N+4

CV WRITE "NACELLE ... HIT STAR AT ..." MESSAGE
      WRITE(IV,214)(INAME(IQ0,IT),IQ0=1,3),J,I8,I7
214   FORMAT(1X,3A4,' NACELLE',I2,' HIT STAR AT (',I2,',',I2,')')

CV NEXT M-O SHIP
213   CONTINUE

CV RETURN
      GO TO 205

CV ======
CV EAGLE
CV ======

209   IF(MIN.NE.14)GO TO 216

CV LOOP OVER ALL LAUNCHED EAGLES
      DO 217 MI=1,LAUNCH

CV IF POSITION I7,I8 NOT POSITION OF EAGLE:
CV SKIP OVER DAMAGE MANAGEMENT FOR EAGLES
      IF(I7.NE.IGLER(MI))GO TO 217
      IF(I8.NE.IGLEC(MI))GO TO 217

CV LOOP OVER ALL M-O SHIPS
      DO 218 N=1,4

CV IF SHIP NOT IN GAME:
CV SKIP "NACELLE ... DESTROYED EAGLE ..." MESSAGE, NEXT SHIP
      IF(ICHOE(N).EQ.0)GO TO 218

CV CALCULATE TERMINAL NUMBER IV
      IV=N+4

CV WRITE MESSAGE "NACELLE ... DESTROYED EAGLE ..."
      WRITE(IV,219)(INAME(IQ0,IT),IQ0=1,3),J,MIN
219   FORMAT(1X,3A4,' NACELLE',I2,' DESTROYED EAGLE',I3)

CV NEXT M-O SHIP
218   CONTINUE

CV EAGLES DAMAGE HANDLING
      CALL BOOM(MIN)

CV RETURN
      GO TO 205

CV NEXT EAGLE
217   CONTINUE

CV ======
CV MINES
CV ======

CV IF MIN > 18: RETURN
216   IF(MIN.GT.18)GO TO 205

CV DELETE MAP ENTRY AT I7,I8
      MAP(I7,I8)=IBLK

CV LOOP OVER ALL M-O SHIPS
      DO 220 N=1,4

CV IF SHIP NOT IN GAME: SKIP "NACELLE ... DESTROYED MINE AT ..." MESSAGE,
CV NEXT SHIP
      IF(ICHOE(N).EQ.0)GO TO 220

CV WRITE MESSAGE "NACELLE ... DESTROYED MINE AT ..."
      WRITE(IV,221)(INAME(IQ0,IT),IQ0=1,3),J,I8,I7
221   FORMAT(1X,3A4,' NACELLE',I2,' DESTROYED MINE AT (',I2,',',I2,')')

CV NEXT M-O SHIP
220   CONTINUE

205   RETURN

      END

C     -- DONALD ECCLESTONE SUBPROGRAM FOR     T R E K 7
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C             - HARDY -
      SUBROUTINE HARDY
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)

CV LOOP OVER ALL M-O SHIPS
      DO 1 I=1,4

CV IF M-O SHIP NOT IN GAME: SKIP MESSAGE
      IF(ICHOE(I).EQ.0)GO TO 1

CV CALCULATE FORTRAN UNIT NUMBER FOR SHIP'S TERMINAL
      J=I+4

CV WRITE MESSAGE
      WRITE(J,2)
2     FORMAT(' DAMAGE FROM SECONDARY EXPLOSIONS-')

CV NEXT M-O SHIP
1     CONTINUE

      RETURN

      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C   SSSS PPPP   OOO   CCCC K   K
C  S     P   P O   O C     K  K
C   SSS  PPPP  O   O C     KKK
C      S P     O   O C     K  K
C  SSSS  P      OOO   CCCC K   K

CV CALCULATE RESULT OF TORPEDO ATTACK (ON TORPEDO BANK AND ENEMY)

      LOGICAL FUNCTION SPOCK(J)
      INTEGER ITZRO,ITVL1
      REAL RTZRO,RTVL1
      DIMENSION IBOLT(4,2),KPLOT(10)
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /H/ANGLE(4),RANG(4),LOCKT(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)
      COMMON /V/IWEB(2),IWEBZ,INVIS(4)
        COMMON /TOM/ITOM
      DATA IBOLT/'PHOT','ON T','ORPE','DO  ','DISR','UPTO','R BO','LT'/
      DATA FUDGE/0.700619195/

CV CALCULATE TORPEDO DAMAGE FACTOR IWOW FROM SHORT-CIRCUIT FACTOR IV1 (???)
      IWOW(X)=IFIX(ALOG((101.-X)*IV1)/FUDGE-1.8185)

CV DEFAULT: TORPEDO NOT SUCCESSFUL
      SPOCK=.FALSE.

CV CALCULATE RANDOM SHORT-CIRCUIT FACTOR (IV1) IN RANGE 1 TO 100
      CALL RANDO(IV1,1,100)

CV RESET TORPEDO LOCK
      LOCKT(IT)=0

CV IF MOVEBILITY OF SHIP = FULL: SET MOVEBILITY TO REDUCED (WHY HERE???)
      IF(IGO(IT).EQ.2)IGO(IT)=1

CV IF SHORT-CIRCUIT FACTOR > 7: SKIP SHORT-CIRCUIT
      IF(IV1.GT.7)GO TO 76

CV ===================================
CV TORPEDO BANKS HAVE SHORT-CIRCUITED
CV ===================================

CV IF SHIP TYPE IS HAVOC/CARNAGE: 100
      IF(JS.EQ.2)GO TO 100

CV ELSE: SHIP TYPE IS ENTERPRISE/POTEMPKIN

CV MESSAGE FOR SHIP TYPE ENTERPRISE
      WRITE(L,78)
78    FORMAT(' PHOTON TORPEDO BANKS HAVE SHORT-CIRCUITED.')
      GO TO 101

CV MESSAGE FOR SHIP TYPE HAVOC/CARNAGE
100   WRITE(L,102)
102   FORMAT(' DISRUPTOR BANKS HAVE SHORT-CIRCUITED.')

101   WRITE(L,103)
103   FORMAT(5X,'THEIR NUMBER HAS DIMINISHED BY ONE-QUARTER')

CV DECREASE NUMBER OF TORPEDOES TO 75%
      IPHOT(IT)=FLOAT(IPHOT(IT))*0.75

CV RETURN
      GO TO 50

CV END OF: TORPEDO BANKS HAVE SHORT-CIRCUITED

CV IF THERE IS NO ACTIVE WEB: TORPEDO TRAVELS FREE (761)
76    IF(IWEB(1).NE.IT.AND.IWEB(2).NE.IT)GO TO 761

CV ELSE: ACTIVE WEB

CV CALCULATE RANDOM WEB PIERCING FACTOR (IV) IN RANGE 1 TO 4
      CALL RANDO(IV,1,4)

CV IF WEB PIERCING FACTOR = 1: TORPEDO TRAVELS FREE (761)
      IF(IV.EQ.1)GO TO 761

CV ELSE: FAILED TO PIERCE THE WEB
      WRITE(L,1001)(IBOLT(J,JS),J=1,4)
1001  FORMAT(1X,4A4,'FAILED TO PIERCE THE WEB')

CV IF WEB PIERCING FACTOR <> 4: NO BOUNCE OFF; RETURN
      IF(IV.NE.4)GO TO 50

CV ELSE (IV=2 OR IV=3): BOUNCED OFF THE WEB AND HAS STRUCK

CV LOOP OVER M-O SHIPS IN RANGE 1 TO 4
      DO 104 I7=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).EQ.0)GO TO 104

CV CALCULATE M-O SHIP FORTRAN UNIT NUMBER I8
      I8=I7+4

CV WRITE MESSAGE
      WRITE(I8,105)(IBOLT(J,JS),J=1,4),(INAME(IQ0,IT),IQ0=1,3)
105   FORMAT(1X,4A4,'BOUNCED OFF THE WEB AND HAS STRUCK THE ',3A4/' DAMA
     *GE REPORT-')

CV NEXT M-O SHIP
104   CONTINUE

CV SET DAMAGE FACTOR TO IWOW(DFLCT(IT)) (TORPEDO DAMAGE FACTOR?)
      IVV=IWOW(DFLCT(IT))

CV MANAGE DAMAGE TO M-O SHIP
      CALL GRUP1(IVV,IT)

CV RETURN
      GO TO 50

CV =====================
CV TORPEDO TRAVELS FREE
CV =====================

CV ???
761   N=3


      ANGLE(IT)=ANGLE(IT)*3.14159265/180.
      IV=0
      AJUST=0.0
        ITZRO=0
        ITVL1=1
        RTZTO=0.0

CV SEE WHAT THE TORPEDO HAS HIT (MIN)
      CALL HORTA(IENTR(IT),IENTC(IT),ITZRO,
     *ITZRO,RANG(IT),ANGLE(IT),J,AJUST,IITVL1,ITZRO,KPLOT)
      ANGLE(IT)=ANGLE(IT)*180./3.14159265
      J=J-1

CV SKIP TORPEDO PATH MESSAGE IF NO INTERMEDIATE STEP
      IF(J.LE.1)GO TO 529

CV WRITE PATH THAT TORPEDO TRAVELS
      WRITE(L,528)(KPLOT(IV),IV=1,J)
528   FORMAT(10(I3,',',I2))

CV IF TARGET IS NOT AN M-O SHIP: 106
529   IF(MIN.GT.4)GO TO 106

CV ===========================
CV TORPEDO TARGET IS M-O SHIP
CV ===========================
CV MIN <=4

CV LOOP OVER M-O SHIPS IN RANGE 1 TO 4
      DO 107 I7=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).EQ.0)GO TO 107

CV CALCULATE M-O SHIP FORTRAN UNIT NUMBER I8
      I8=I7+4

CV WRITE "HIT ON THE <M-O SHIP>"
      WRITE(I8,108)(INAME(IQ0,IT),IQ0=1,3),(IBOLT(J,JS),J=1,4),
     *(INAME(IQ0,MIN),IQ0=1,3)
108   FORMAT(1X,3A4,1X,4A4,' HAS SCORED A HIT ON THE ',3A4)

CV NEXT M-O SHIP
107   CONTINUE

CV SET DAMAGE FACTOR TO IWOW(DFLCT(IT)) (TORPEDO DAMAGE FACTOR)
      IVV=IWOW(DFLCT(MIN))

CV MANAGE DAMAGE TO M-O SHIP
      CALL GRUP1(IVV,MIN)

CV TORPEDO SUCCESSFUL
      SPOCK=.TRUE.

CV RETURN
      GO TO 50

106   IF(MIN.GT.12)GO TO 109

CV ===========================================
CV TORPEDO TARGET IS STARBASE OR C-O SHIP 1-6
CV ===========================================
CV 4 < MIN <= 12
      MIN=MIN-4
      DO 110 I7=1,4
      IF(ICHOE(I7).EQ.0)GO TO 110
      I8=I7+4
      WRITE(I8,111)(INAME(IQ0,IT),IQ0=1,3),(IBOLT(J,JS),J=1,4),(IENM2(J,
     *MIN),J=1,4)
111   FORMAT(1X,3A4,1X,4A4,'HIT ',4A4)
110   CONTINUE

CV SET DAMAGE FACTOR TO IWOW(DFLCT(IT)) (TORPEDO DAMAGE FACTOR)
      IVV=IWOW(DFLCK(MIN))

CV MANAGE DAMAGE TO C-O SHIP
      CALL GRUP3(IVV,MIN)

CV TORPEDO SUCCESSFUL
      SPOCK=.TRUE.

CV RETURN
      GO TO 50

109   IF(MIN.NE.13)GO TO 112

CV =======================
CV TORPEDO TARGET IS STAR
CV =======================
CV MIN = 13

CV LOOP OVER STARBASES IN RANGE 1 TO 2
      DO 119 IV=1,2

CV IF STARBASE ROW <> TARGET ROW: NEXT STARBASE
      IF(IBASR(IV).NE.I7)GO TO 119

CV IF STARBASE COL = TARGET COL: HIT STARBASE
      IF(IBASC(IV).EQ.I8)GO TO 120

CV NEXT STARBASE
119   CONTINUE

CV ELSE: HIT STAR

CV WRITE MESSAGE "<WEAPON TYPE> HAS SCORED A HIT ON A STAR"
      WRITE(L,526)(IBOLT(J,JS),J=1,4)
526   FORMAT(1X,4A4,'HAS SCORED A HIT ON A STAR')

CV THIS IS NOT CONSIDERED A "SUCCESSFUL HIT"!

CV RETURN
      GO TO 50

CV ===========================
CV TORPEDO TARGET IS STARBASE
CV ===========================

CV LOOP OVER M-O SHIP IN RANGE 1 TO 4
120   DO 121 I7=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).EQ.0)GO TO 121

CV CALCULATE M-O SHIP FORTRAN UNIT
      I8=I7+4

CV WRITE MESSAGE <M-O SHIP> HAS HIT <STARBASE OWNER PARTY> STARBASE <NUMBER>
      WRITE(I8,122)(INAME(IQ0,IT),IQ0=1,3),(IBOLT(J,JS),J=1,4),
     *(ISIDE(IQ0,IV),IQ0=1,3),IBASE(IV)
122   FORMAT(1X,3A4,1X,4A4,' HAS HIT ',3A4,' STARBASE',I3)

CV NEXT M-O SHIP
121   CONTINUE

CV SET DAMAGE FACTOR TO IWOW(DFLCT(IT)) (TORPEDO DAMAGE FACTOR)
      IVV=IWOW(DFLCB(IV)/3.)

CV MANAGE DAMAGE TO STARBASE MACHINE
      CALL GRUP2(IVV,IV)

CV TORPEDO SUCCESSFUL
      SPOCK=.TRUE.

CV RETURN
      GO TO 50

112   IF(MIN.NE.14)GO TO 113

CV ========================
CV TORPEDO TARGET IS EAGLE
CV ========================
CV MIN = 14

CV LOOP OVER EAGLES (IV) RANGE 1 TO LAUNCH
      DO 5541 IV=1,LAUNCH

CV IF EAGLE ROW <> TARGET ROW: NEXT EAGLE
      IF(IGLER(IV).NE.I7)GO TO 5541

CV IF EAGLE COL <> TARGET COL: HIT EAGLE
      IF(IGLEC(IV).EQ.I8)GO TO 5542

CV NEXT EAGLE
5541  CONTINUE

CV HIT EAGLE

CV LOOP OVER M-O SHIP IN RANGE 1 TO 4
5542  DO 114 I7=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).EQ.0)GO TO 114

CV CALCULATE M-O SHIP FORTRAN UNIT
      I8=I7+4

CV WRITE MESSAGE "<M-O SHIP> <WEAPON TYPE> DESTROYED EAGLE <NUMBER>"
      WRITE(I8,115)(INAME(IQ0,IT),IQ0=1,3),(IBOLT(J,JS),J=1,4),IV
115   FORMAT(1X,3A4,1X,4A4,' DESTROYED EAGLE',I3)

CV NEXT M-O SHIP
114   CONTINUE

CV EAGLES DAMAGE HANDLING
      CALL BOOM(IV)

CV TORPEDO SUCCESSFUL
      SPOCK=.TRUE.

CV RETURN
      GO TO 50

113   IF(MIN.GT.18)GOTO 116

CV =======================
CV TORPEDO TARGET IS MINE
CV =======================
CV 14 < MIN <= 18 (???)

      WRITE(L,117)(IBOLT(J,JS),J=1,4),I8,I7
117   FORMAT(1X,4A4,'DETONATED MINE AT (',I2,',',I2,')')
      MAP(I7,I8)=IBLK

CV THIS IS NOT CONSIDERED A "SUCCESSFUL HIT"!

CV RETURN
      GO TO 50

CV MIN >18

CV MIN 19/20/21/...

116   IF(MIN.NE.20)GO TO 118

CV MIN = 20

555   J=I7
      IVV=I8
      N=2
      IV=0
        RTVL1=1.5

      CALL HORTA(J,IVV,IENTR(IT),IENTC(IT),
     *RTVL1,ITZRO,ITZRO,ITZRO,ITVL1,ITZRO,ITZRO)
      IF(MIN.EQ.20)GO TO 555
118   IF(I8.EQ.IENTC(IT).AND.I7.EQ.IENTR(IT))GO TO 557
      WRITE(L,81)(IBOLT(J,JS),J=1,4),I8,I7
81    FORMAT(1X,4A4,'FAILED TO HIT ANYTHING'/' NOW A MINE AT (',I2,',',
     *I2,')')
      MINES=MINES+1
      MAP(I7,I8)=IM(IT)
      GO TO 50
557   WRITE(L,806)(IBOLT(J,JS),J=1,4)
806   FORMAT(1X,4A4,'LOST')
50    RETURN
      END


C     -- DONALD ECCLESTONE SUBPROGRAM FOR     T R E K 7--
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C             - SAREK -
C

CV CALCULATE RESULT OF PHASER ATTACK

      LOGICAL FUNCTION SAREK(J)
      LOGICAL MUDD
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /G/ZAP(4),ICOLA(4),IROWA(4),LOCK(4),ICOIL(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)
      DATA FUDGE/0.700619195/

CV WRONG PLACEMENT OF BRACKETS!
CV      IWOX(X)=IFIX(ALOG((101.-X)*IV*ZAP(IT)/FLOAT(N)/FUDGE-10.4915))
      IWOX(X)=IFIX(ALOG((101.-X)*IV*ZAP(IT)/FLOAT(N))/FUDGE-10.4915)
      SAREK=.FALSE.

CV CALCULATE RANDOM PHASER DEFECT FACTOR IV
520   CALL RANDO(IV,1,100)

CV UNLOCK PHASER
      LOCK(IT)=0

CV IF MOVEBILITY OF SHIP = FULL: SET MOVEBILITY TO REDUCED
      IF(IGO(IT).EQ.2)IGO(IT)=1

CV 95% (100-5) PROBABILITY OF NO SHORT-CIRCUIT: 29
      IF(IV.GT.4)GO TO 29

CV REDUCE PHASER POWER TO 75%
      PHASR(IT)=PHASR(IT)*0.75
      WRITE(L,31)
31    FORMAT(' PHASER BANKS HAVE SHORT-CIRCUITED AND ARE REDUCED'/' TO 3
     */4 THEIR ORIGINAL ENERGY')

CV RETURN
      GO TO 50

CV 91% (100-9) PROBABILITY OF NO BURNOUT: 1010
29    IF(IV.GT.8)GO TO 1010

CV SET PHASER COIL BURNOUT
      ICOIL(IT)=1
      WRITE(L,1011)
1011  FORMAT(' MAIN COIL BURNOUT IN THE PHASER BANKS-'/' PHASERS ARE NOW
     * INOPERATIVE UNTIL REPAIRS ARE COMPLETE')

CV RETURN
      GO TO 50

CV PHASER NEITHER SHORT-CIRCUITED NOR MAIN COIL BURNOUT

CV CALCULATE DISTANCE (N) BETWEEN M-O SHIP AND PHASER TARGET
1010  N=DI(IENTR(IT),IENTC(IT),IROWA(IT),ICOLA(IT))

CV SHOOTING AT YOURSELF (DISTANCE = 0) DOES NOTHING:
CV WRITE MESSAGE "PHASER BEAM MISSED"
      IF(N.EQ.0)GO TO 125

CV SET J TO ROW DISTANCE BETWEEN M-O SHIP AND PHASER TARGET
      J=IROWA(IT)-IENTR(IT)

CV SET J TO COL DISTANCE BETWEEN M-O SHIP AND PHASER TARGET
      IV1=ICOLA(IT)-IENTC(IT)

CV SET IVV (?) TO 0
      IVV=0

CV LOOP OVER M-O SHIPS I FOR 1 TO 4
      DO 123 I=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I).EQ.0)GO TO 123

CV IF M-O SHIP I = ACTING M-O SHIP: NEXT M-O SHIP
      IF(I.EQ.IT)GO TO 123

CV SET I7 TO ROW DISTANCE BETWEEN M-O SHIP I AND ACTING M-O SHIP
      I7=IENTR(I)-IENTR(IT)

CV SET I8 TO COL DISTANCE BETWEEN M-O SHIP I AND ACTING M-O SHIP
      I8=IENTC(I)-IENTC(IT)

CV IF PHASER HIT...: IVV=1
      IF(MUDD(J,IV1,I7,I8,N))IVV=I

CV NEXT M-O SHIP
123   CONTINUE

CV LOOP OVER C-O SHIP I FOR 1 TO 8
      DO 780 I=1,8

CV IF C-O SHIP NOT IN GAME: NEXT C-O SHIP
      IF(ICHOS(I).EQ.0)GO TO 780

CV SET I7 TO ROW DISTANCE BETWEEN C-O SHIP I AND ACTING M-O SHIP
      I7=IKLNR(I)-IENTR(IT)

CV SET I7 TO COL DISTANCE BETWEEN C-O SHIP I AND ACTING M-O SHIP
      I8=IKLNC(I)-IENTC(IT)

CV IF ...: IVV=I+4
      IF(MUDD(J,IV1,I7,I8,N))IVV=I+4

CV NEXT C-O SHIP
780   CONTINUE

CV LOOP OVER ALL STARBASES (2)
      DO 124 I=1,2

      IF(ICHOB(I).EQ.0)GO TO 124
      I7=IBASR(I)-IENTR(IT)
      I8=IBASC(I)-IENTC(IT)
      IF(MUDD(J,IV1,I7,I8,N))IVV=I+12
124   CONTINUE
      IF(NUMOUT.EQ.0)GO TO 34
      DO 760 I=1,LAUNCH
      IF(IGLER(I).EQ.0)GO TO 760
      I7=IGLER(I)-IENTR(IT)
      I8=IGLEC(I)-IENTC(IT)
      IF(MUDD(J,IV1,I7,I8,N))IVV=I+14
760   CONTINUE
34    I=IVV

CV IF DAMAGE (I) >0 AND 2.5*DISTANCE <= IV: PHASER BEAM HIT
      IF(I.GT.0.AND.2.5*FLOAT(N).LE.IV)GO TO 801

CV ELSE: PHASER BEAM MISSED

CV WRITE MESSAGE "PHASER BEAM MISSED"
125   WRITE(L,33)
33    FORMAT(' PHASER BEAM MISSED')

CV RETURN
      GO TO 50

CV PHASER HIT

CV ==========
CV M-O SHIPS
CV ==========

CV IF VESSEL INDEX > 4: ALL VESSELS EXCEPT M-O SHIPS (1250)
801   IF(I.GT.4)GO TO 1250

      DO 126 I7=1,4
      IF(ICHOE(I7).EQ.0)GO TO 126
      I8=I7+4
      WRITE(I8,127)(INAME(IQ0,IT),IQ0=1,3),(INAME(IQ0,I),IQ0=1,3)
127   FORMAT(1X,3A4,' PHASER BEAM HIT ',3A4)
126   CONTINUE

CV GENERATE IVV DAMAGE NUMBER DEPENDENT ON DEFLECTOR STRENGTH
      IVV=IWOX(DFLCT(I))

CV MANAGE M-O SHIP DAMAGE
      CALL GRUP1(IVV,I)

CV SET PHASER HIT FLAG SAREK=.TRUE., RETURN (500)
      GO TO 500

CV =====================
CV C-O SHIPS & DOOMSDAY
CV =====================

CV IF VESSEL INDEX > 12: ALL VESSELS EXCEPT M-O & C-O SHIPS (128)
1250  IF(I.GT.12)GO TO 128
      I=I-4

CV IF C-O SHIP IS NOT DOOMSDAY: SKIP DOOMSDAY (135)
      IF(I.NE.7)GO TO 135

CV CALCULATE RANDOM DOOMSDAY DAMAGE PROBABILITY FACTOR IV1 IN RANGE 1 TO 3
      CALL RANDO(IV1,1,3)

CV 66% (2/3) PROBABILITY THAT PHASER BOUNCES OFF DOOMSDAY

CV IF DOOMSDAY DAMAGE PROBABILITY FACTOR (IV1) = 1: SKIP BOUNCE OFF DOOMSDAY (135)
      IF(IV1.EQ.1)GO TO 135
      WRITE(L,776)
776   FORMAT(' PHASER BEAM BOUNCED OFF DOOMSDAY MACHINE')

CV RETURN
      GO TO 50

CV CALCULATE WHICH ENEMY IS HIT

CV LOOP OVER M-O SHIPS I7 FOR 1 TO 4
135   DO 129 I7=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).EQ.0)GO TO 129

      I8=I7+4
      WRITE(I8,130)(INAME(IQ0,IT),IQ0=1,3),(IENM2(J,I),J=1,4)
130   FORMAT(1X,3A4,' PHASER BEAM HIT ',4A4)

CV NEXT M-O SHIP
129   CONTINUE

CV GENERATE IVV DAMAGE NUMBER DEPENDENT ON DEFLECTOR STRENGTH
      IVV=IWOX(DFLCK(I))

CV MANAGE C-O SHIP DAMAGE
      CALL GRUP3(IVV,I)

CV SET PHASER HIT FLAG SAREK=.TRUE., RETURN (500)
      GO TO 500

CV ==========
CV STARBASES
CV ==========

CV IF EAGLE: 131
128   IF(I.GT.14)GO TO 131

CV I<12
      I=I-12

CV LOOP OVER M-O SHIPS I7 FOR 1 TO 4
      DO 132 I7=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(I7).EQ.0)GO TO 132

      I8=I7+4
      WRITE(I8,133)(INAME(IQ0,IT),IQ0=1,3),(ISIDE(IQ0,I),IQ0=1,3),
     *IBASE(I)
133   FORMAT(1X,3A4,' PHASER BEAM HIT ',3A4,' STARBASE',I3)

CV NEXT M-O SHIP
132   CONTINUE

CV =======
CV EAGLES
CV =======

      DISTP=DFLCB(I)/3.
      IVV=IWOX(DISTP)
      CALL GRUP2(IVV,I)
      GO TO 500
131   I=I-14
      DO 134 I7=1,4
      IF(ICHOE(I7).EQ.0)GO TO 134
      I8=I7+4
      WRITE(I8,1350)(INAME(IQ0,IT),IQ0=1,3),I
1350  FORMAT(1X,3A4,' PHASER BEAM DESTROYED EAGLE',I3)
134   CONTINUE

      CALL BOOM(I)
500   SAREK=.TRUE.
50    RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/26/2000 BY: D.G.
C
C - MUDD
C

CV HIT OR MISS?

      LOGICAL FUNCTION MUDD(J,IV1,I7,I8,N)
      MUDD=.FALSE.

      IF(IV1.EQ.0)GO TO 1014

      IF(I8.NE.0)GO TO 1015

1014  IF(IV1.NE.I8)GO TO 780

      IF(IABS(J)/J.EQ.IABS(I7)/I7)GO TO 1016
      GO TO 780

1015  IF(J.EQ.0)GO TO 1013

      IF(I7.NE.0)GO TO 1017

1013  IF(J.NE.I7)GO TO 780

      IF(IABS(IV1)/IV1.EQ.IABS(I8)/I8)GO TO 1016
      GO TO 780

1017  IF(FLOAT(IV1)/FLOAT(J).NE.FLOAT(I8)/FLOAT(I7))GO TO 780

      IF(IABS(IV1)/IV1.NE.IABS(I8)/I8)GO TO 780

1016  MIN=SQRT(FLOAT(I8**2+I7**2))

      IF(MIN.GT.N)GO TO 780

      J=I7
      IV1=I8
      N=MIN
      MUDD=.TRUE.
780   RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C - HARPO -
C

CV BOARDING HANDLING ROUTINE
CV ==========================
CV INPUT TRANSFER PARAMETERS
CV ==========================
CV L      - FORTRAN UNIT NUMBER OF M-O SHIP HAVING ITS TURN
CV
CV ===================
CV INTERNAL VARIABLES
CV ===================
CV
CV ==================
CV OUTPUT PARAMETERS
CV ==================
CV

      LOGICAL FUNCTION HARPO(IBORD,NUM,IROW,ICOL)
      LOGICAL CHICO
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /B/NDEAB(2),IPHOB(2),IONB(2),IGOB(2),IFIB(2)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /O/MA(4,33),K(4,14),NOMAP(4),MANUM(4),HIVEL(4,2),ITEMP(4),
     *NOMOV(4)
      COMMON /Q/IARMZ(6),INDUZ,IXRYZ,IMAGZ,IMAGRZ,IABSZ,ISTAZ,INVIZ(4),
     *ICLOZ,IONNO,IPLANZ,IRUNZ,IDEVZ,IDRIZ,IMAXZ
      COMMON /R/IBPSC(4),IBPSB(8),IBPSS(2),IBPSE(25)
      COMMON /S/IBPOB(8),IBPOE(25)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)
      COMMON /V/IWEB(2),IWEBZ,INVIS(4)

CV SET BOARDING SUCCESSFUL FLAG (HARPO) TO FALSE
      HARPO=.FALSE.
      IBORD=0

CV SET TRANSPORTER DECK ID TO 7
      JTK=7

CV IF SHIP TYPE = 2 (HAVOC/CARNAGE): SET TRANSPORTER DECK ID TO 26
      IF(JS.EQ.2)JTK=26

CV SET FORTRAN UNIT NUMBER TO 0 (WHY?)
      I8=0

CV IF DAMAGE OF TRANSPORTER ROOMS OF OPPONENT PARTY <9:
CV SKIP MESSAGE "TRANSPORTER ROOMS DESTROYED"
      IF(MA(IT,JTK).LT.9)GO TO 100

CV ELSE: WRITE MESSAGE "TRANSPORTER ROOMS DESTROYED"; RETURN
2     WRITE(L,3)
3     FORMAT(' TRANSPORTER ROOMS DESTROYED - CANNOT BEAM A BOARDING
     * PARTY')
5     RETURN

CV IF NOONE TO BOARD: RETURN
100   IF(NUM.EQ.0)RETURN

CV IF NUMBER OF CREW TO BOARD > CREW: WRITE MESSAGE
CV "NUMBER IN PARTY GREATER THAN CREW ONBOARD"; RETURN
      IF(NUM.LE.NDEAD(IT))GO TO 301
119   WRITE(L,118)
118   FORMAT(' NUMBER IN PARTY GREATER THAN CREW ONBOARD -OPERATION CANC
     *ELLED')
      RETURN

CV IF BOARDING TARGET DISTANCE <=5:
CV SKIP MESSAGE "BOARDING TARGET OUT OF TRANSPORTER RANGE"
301   IF((IENTC(IT)-ICOL)**2+(IENTR(IT)-IROW)**2.LE.25)GO TO 302

CV ELSE: WRITE MESSAGE "BOARDING TARGET OUT OF TRANSPORTER RANGE"
102   WRITE(L,103)
103   FORMAT(' BOARDING TARGET OUT OF TRANSPORTER RANGE -OPERATION CANCE
     *LLED')
      RETURN

CV IF MOVEBILITY OF OPPONENT SHIP = FULL: SET MOVEBILITY TO REDUCED
302   IF(IGO(IT).EQ.2)IGO(IT)=1

CV WRITE MESSAGE "BOARDING PARTY OPERATION NOW COMMENCING"
      WRITE(L,101)
101   FORMAT(' BOARDING PARTY OPERATION NOW COMMENCING')

CV ====================
CV BOARDING INITIATION
CV ====================

CV LOOP OVER C-O SHIPS I FROM 1 TO 8
      DO 104 I=1,8

CV IF BOARD-TO POSITION = C-O SHIP POSITION: BOARD TO C-O SHIP (105)
      IF(IROW.EQ.IKLNR(I).AND.ICOL.EQ.IKLNC(I))GO TO 105

CV NEXT C-O SHIP
104   CONTINUE

CV LOOP OVER M-O SHIPS I FROM 1 TO 4
      DO 215 I=1,4

CV IF BOARD-TO POSITION = M-O SHIP POSITION: BOARD TO M-O SHIP (303)
      IF(IROW.EQ.IENTR(I).AND.ICOL.EQ.IENTC(I))GO TO 303

CV NEXT M-O SHIP
215   CONTINUE

CV IF BOARD-TO POSITION = EAGLE POSITION: BOARD TO EAGLE (130)
      IF(MAP(IROW,ICOL).EQ.IGLE)GO TO 130

CV IF BOARD-TO POSITION = STAR POSITION: BOARD TO STAR (107)
      IF(MAP(IROW,ICOL).EQ.ISTAR)GO TO 107

CV NUMBER OF CREW < 0:RETURN
      IF(NUM.LT.0)RETURN

CV ====================
CV BOARD TO DEEP SPACE
CV ====================
      WRITE(L,106)
106   FORMAT(' BOARDING PARTY MATERIALIZED IN DEEP SPACE -ALL HAVE PERIS
     *HED')

CV REDUCE CREW OF SHIP THAT WAS BOARDED FROM BY NUMBER OF BOARDED CREW
      NDEAD(IT)=NDEAD(IT)-NUM
      RETURN

CV ==================
CV BOARD TO C-O SHIP
CV ==================

CV IF C-O SHIP IS NOT A DOOMSDAY MACHINE: DO BOARDING;
CV IF BOARDING NOT SUCCESSFUL: RETURN (304)
105   IF(I.NE.7)GO TO 304

CV ELSE: WRITE MESSAGE "TRANSPORTER BEAM BOUNCED OFF DOOMSDAY MACHINE"
      WRITE(L,113)
113   FORMAT('TRANSPORTER BEAM BOUNCED OFF DOOMSDAY MACHINE')

CV IF NUMBER OF CREW TO BOARD > 0:
CV REDUCE CREW OF SHIP THAT WAS BOARDED FROM BY NUMBER OF BOARDED CREW
500   IF(NUM.GT.0)NDEAD(IT)=NDEAD(IT)-NUM
      RETURN

CV DO BOARDING; IF BOARDING NOT SUCCESSFUL: RETURN
304   IF(.NOT.CHICO(NUM,ICOL,IROW,IBPOB(I),DFLCK(I),IBPSB(I),IBPOB(7),
     *NDEAD(IT),1))RETURN

CV ELSE: SET BOARDING SUCCESSFUL FLAG TO TRUE
      HARPO=.TRUE.

CV IF NO STARBASE CONTROL LOSS (NO ONE ONBOARD):
CV SKIP STARBASE CONTROL LOSS (135)
      IF(IBPOB(I).NE.0)GO TO 135

CV STARBASE CONTROL LOSS
      CALL TPAU(I)

CV WEB INTERACTION
135   CALL OXMYX

CV IF C-O SHIP IS NOT KZINTI: RETURN (143)
      IF(I.NE.6)GO TO 143

CV =========================
CV ELSE: C-O SHIP IS KZINTI
CV =========================

CV SET KZINTI TYPE OF STRATEGY TO 1 (=ATTACK/RETREAT)
      IPLANZ=1

CV KZINTI DEFLECTOR DISADVANTAGE BEFORE RETREAT = 150
      IRUNZ=150

CV KZINTI ATTACK/RETEAT ANGLE = 0
      IDEVZ=0

143   RETURN

CV ==================
CV BOARD TO M-O SHIP
CV ==================

CV IF IF BOARDING SHIP = TARGET SHIP:
CV SKIP MESSAGE "BOARDING PARTY MATERIALIZED IN OUR SHIP",
CV SKIP SETTING BOARDING FLAG TO "TRUE", RETURN

CV ELSE: BOARDED TO SAME SHIP
303   IF(I.NE.IT)GO TO 306
      WRITE(L,111)
111   FORMAT(' BOARDING PARTY MATERIALIZED IN OUR SHIP')

CV BOARDING SUCCESSFUL
      HARPO=.TRUE.

      RETURN

CV SET I6 = BOARDER PARTY
306   I6=(IBPSC(I)+1)/2

CV DO BOARDING; IF BOARDING NOT SUCCESSFUL: RETURN
      IF(.NOT.CHICO(NUM,ICOL,IROW,NDEAD(I),DFLCT(I),I6,I5,NDEAD(IT),2))
     *RETURN

CV ELSE: BOARDING SUCCESSFUL
      HARPO=.TRUE.

CV DO WEB INTERACTION (135)
      GO TO 135

CV ===============
CV BOARD TO EAGLE
CV ===============

CV LOOP OVER LAUNCHED EAGLES I FOR 1 TO LAUNCH
130   DO 131 I=1,LAUNCH

CV IF EAGLE IS NOT IN GAME: NEXT EAGLE
      IF(IGLER(I).EQ.0)GO TO 131

CV IF BOARD-TO POSITION = EAGLE POSITION: 132
      IF(ICOL.EQ.IGLEC(I).AND.IROW.EQ.IGLER(I))GO TO 132

CV ELSE: NEXT EAGLE
131   CONTINUE

CV SET DEFLECTOR STRENGTH OF TARGET TO "0"
132   DISTP=0.

CV DO BOARDING; IF BOARDING NOT SUCCESSFUL: 137
      IF(.NOT.CHICO(NUM,ICOL,IROW,IBPOE(I),DISTP,IBPSE(I),NUME(IS),NDEAD
     *(IT),3))GO TO 137

CV ELSE: BOARDING SUCCESSFUL
      HARPO=.TRUE.

CV IF NUMBER OF CREW ON EAGLE <= 20: 308
      IF(IBPOE(I).LE.20)GO TO 308

CV ELSE: EAGLE HAS BURST, GO TO 142
      WRITE(L,141)I
141   FORMAT(' SO MANY PEOPLE HAVE MATERIALIZED ONTO EAGLE',I3,' THAT IT
     * HAS BURST')
      GO TO 142

137   IF(DISTP.GE.0)RETURN

CV WRITE EAGLE DESTRUCTION MESSAGE
142   WRITE(L,136)I
136   FORMAT(' EAGLE',I3,' DESTROYED')

CV BOARDING FAILD
      HARPO=.FALSE.

CV EAGLE DESTRUCTION
      CALL BOOM(I)
      RETURN

CV EAGLE SURVIVES, BOARDING SUCCESSFUL

CV IF IBPOE(I).EQ.0: MANAGE EAGLE CONTROL LOSS
308   IF(IBPOE(I).EQ.0)CALL TPRING(I)
      RETURN

CV =======================
CV BOARD TO STAR/STARBASE
CV =======================

CV LOOP OVER STARBASES (I) FOR 1 TO 2
107   DO 210 I=1,2

CV IF POSITION OF STARBASE = BOARDING POSITION: BOARDING TO STARBASE
      IF(IROW.EQ.IBASR(I).AND.ICOL.EQ.IBASC(I))GO TO 307

CV NEXT STARBASE
210   CONTINUE

CV ========================
CV BOARDING TO/FROM A STAR
CV ========================

CV BOARDING NUMBER <0: BOARDING FROM TARGET ("GET"): 309
      IF(NUM.LT.0)GO TO 309

CV ELSE BOARDING TO A STAR; MESSAGE
      WRITE(L,108)
108   FORMAT(' BOARDING PARTY MATERIALIZED IN A STAR -ALL HAVE SIZZLED')

CV REDUCE CREW OF SHIP THAT WAS BOARDED FROM BY NUMBER OF BOARDED CREW
      GO TO 500

CV BOARDING FROM A STAR, MESSAGE
309   WRITE(L,204)
204   FORMAT(' 2 TRANSPORTER TECHNICIANS KILLED WHEN PLASMA FROM STAR MA
     *TERIALIZED'/' IN TRANSPORTER CHAMBER')

CV DECREASE NUMBER OF CREW BY 2
      NDEAD(IT)=NDEAD(IT)-2

CV RETURN
      RETURN

CV ============================
CV BOARDING TO/FROM A STARBASE
CV ============================

CV IF BOARDING NOT SUCCESSFUL: RETURN
307   IF(.NOT.CHICO(NUM,ICOL,IROW,NDEAB(I),DFLCB(I),IBPSS(I),I5,NDEAD(IT
     *),4))RETURN

CV ELSE: BOARDING SUCCESSFUL
      HARPO=.TRUE.

CV IF BOARDING FROM TARGET AND NO CREW LEFT:
CV WRITE MESSAGE
      IF(NUM.LT.0.AND.NDEAB(I).EQ.0)WRITE(L,213)(ISIDE(IQ0,I),IQ0=1,3),
     *IBASE(I)
213   FORMAT(1X,3A4,' STARBASE',I3,' EVACUATED')
      RETURN
      END



C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C
C - TPRING -
C
C THIS SUBROUTINE WAS HAND TYPED BY DG NOV. 17/1999
C

CV EAGLE CONTROL LOST - I.E. SUCCESSFULLY BOARDED BY OPPONENT PARTY

      SUBROUTINE TPRING(J)
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /R/IBPSC(4),IBPSB(8),IBPSS(2),IBPSE(25)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)

CV LOOP OVER M-O SHIPS IV FOR 1 TO 4
      DO 142 IV=1,4

CV IF M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(IV).EQ.0)GO TO 142

CV CALCULATE FORTRAN UNIT NUMBER FOR TERMINAL
      I7=IV+4

CV WRITE MESSAGE
      WRITE(I7,208)J
208   FORMAT(' CONTROL OF EAGLE',I3,' HAS BEEN LOST')

CV NEXT M-O SHIP
142   CONTINUE

CV DECREMENT NUMBER OF GAME PARTICIPANTS OF PARTY THAT LOSES AN EAGLE (J)
      II(IBPSE(J))=II(IBPSE(J))-1

CV DECREMENT NUMBER OF EAGLES OF OWNER PARTY OF EAGLE "J"
      NUME(IBPSE(J))=NUME(IBPSE(J))-1

CV SET EAGLE INDEX "J" TO O (OUT OF CONTROL?)
      IBPSE(J)=0

CV INCREASE NUMBER OF C-O VESSEL (I3) BY 1
CV (EAGLE NOW UNDER CONTROL OF C-O VESSEL)
      I3=I3+1
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C - CHICO -
C

CV CREW BOARDING EXECUTION
CV
CV ==========================
CV INPUT TRANSFER PARAMETERS
CV ==========================
CV NUM    - NUMBER OF CREW TO BOARD
CV ICOL   - TARGET COL
CV IROW   - TARGET ROW
CV IHORDE - CREW ON TARGET
CV DEFLT  - DEFLECTOR STRENGTH OF TARGET
CV ISID   - BOARDING TARGET INDEX
CV IS     - ACTOR (BOARDER) INDEX
CV ICNT   - ?
CV IFROM  - CREW ON SOURCE
CV J      - BOARD TO: 1=C-O SHIP, 2=M-O SHIP, 3=EAGLE, 4=STARBASE
CV L      - FORTRAN UNIT NUMBER OF M-O SHIP HAVING ITS TURN
CV
CV ===================
CV INTERNAL VARIABLES
CV ===================
CV IDEKU  - DECK INTRUSION INDEX
CV
CV ==================
CV OUTPUT PARAMETERS
CV ==================
CV

      LOGICAL FUNCTION CHICO(NUM,ICOL,IROW,IHORDE,DEFLT,ISID,ICNT,
     *IFROM,J)
      LOGICAL ZEPPO
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /O/MA(4,33),K(4,14),NOMAP(4),MANUM(4),HIVEL(4,2),ITEMP(4),
     *NOMOV(4)
      COMMON /R/IBPSC(4),IBPSB(8),IBPSS(2),IBPSE(25)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)

CV SET DEFAULT: BOARDING NOT SUCCESSFUL
      CHICO=.FALSE.

CV IF VESSEL TO BOARD TO ("TARGET") IS NOT CONTROLLED BY OPPONENT
CV (= OWN PARTY: 100)
      IF(ISID.NE.IS)GO TO 100

CV ==========================================
CV BOARDING TARGET IS CONTROLLED BY OPPONENT
CV ==========================================

CV IF NUMBER OF CREW TO BE BOARDED IS POSITIVE (BOARD TO TARGET):
CV DO BOARD TO TARGET (50)
      IF(NUM.GT.0)GO TO 50

CV =====================
CV DO BOARD FROM TARGET
CV =====================

CV IF ENOUGH CREW TO BOARD FROM TARGET: SKIP BOARDING CANCELLATION
      IF(IHORDE+NUM.GE.0)GO TO 10

CV ELSE: BOARDING FROM TARGET CANCELLATION
      WRITE(L,2)
2     FORMAT(' NUMBER IN PARTY GREATER THAN CREW ONBOARD -OPERATION CANC
     *ELLED')
      RETURN

CV ENOUGH CREW TO BOARD FROM TARGET

CV IF TARGET IS NOT 2=M-O SHIP: PROCESS BOARDING AND RETURN (1)
10    IF(J.NE.2)GO TO 1

CV BOARDING TARGETS LEFT: 2=M-O SHIP, 3=EAGLE (OWN), 4=STARBASE (OWN)

CV CALCULATE "BOARD FROM" SHIP FORTRAN UNIT NUMBER I8
      I8=IBPSC(I)+4

CV NUMBER OF CREW TO BEAM OFF = -NUMBER OF CREW TO BOARD
      IV=-NUM

CV ASK FOR ALLOWANCE TO BEAM OFF
      WRITE(I8,13)(INAME(IQ0,IT),IQ0=1,3),IV
13    FORMAT(' DO YOU WISH THE ',3A4,' TO BEAM OFF',I5,' OF YOUR CREW?')
      READ(I8,14)NA
14    FORMAT(A1)

CV IF BEAM OFF ALLOWED: SKIP MESSAGE "NOT ALLOWED"
      IF(NA.EQ.'Y')GO TO 1

CV WRITE MESSAGE "NOT ALLOWED"; RETURN
      WRITE(L,15)(INAME(IQ0,I),IQ0=1,3)
15    FORMAT(1X,3A4,' CAPTAIN DOES NOT WANT TO SPARE HIS CREW')
      RETURN

CV ELSE: BEAM OFF ALLOWED

CV ===========================================
CV CALCULATE CREW NUMBERS ON TARGET & SOURCE,
CV SET BOARDING SUCCESSFUL FLAG, RETURN
CV ===========================================

CV CALCULATE NUMBER OF CREW REMAINING ON TARGET (IHORDE)
1     IHORDE=IHORDE+NUM

CV CALCULATE NUMBER OF CREW REMAINING ON SOURCE (IFROM)
      IFROM=IFROM-NUM

CV SET BOARDING SUCCESSFUL FLAG
      CHICO=.TRUE.

CV RETURN
      RETURN

CV ===================
CV DO BOARD TO TARGET
CV ===================

50    WRITE(L,51)NUM
51    FORMAT(' CREW ON SHIP REINFORCED BY',I4)

CV IF BOARD TO=4=STARBASE AND CREW REMAINING ON TARGET = 0:
CV II(IS)=II(IS)+1 (?)
      IF(J.EQ.4.AND.IHORDE.EQ.0)II(IS)=II(IS)+1

CV IF TARGET IS NOT 2=M-O SHIP:
CV CALCULATE CREW NUMBERS ON TARGET & SOURCE,
CV SET BOARDING SUCCESSFUL FLAG, RETURN (1)
      IF(J.NE.2)GO TO 1

CV CALCULATE "SUCCESSFUL BORDER" SHIP FORTRAN UNIT NUMBER I8
      I8=IBPSC(I)+4

CV IF BOARD TO M-O SHIP NOT EMPTY: WRITE MESSAGE ABOUT NEW CREW (11)
      IF(ICHOE(I).NE.2)GO TO 11

CV =========================
CV TARGET M-O SHIP IS EMPTY
CV =========================

CV SET TARGET M-O SHIP FLAG TO "NOT EMPTY"
      ICHOE(I)=1

CV SET TARGET M-O SURRENDER FLAG TO "NO SURRENDER"
      ISURR(I)=0

CV INCREMENT NUMBER OF PARTICIPANTS (SHIPS AND STARBASES)
CV OF PARTY OF (ACTING SHIP)
      II(IS)=II(IS)+1

CV INCREMENT NUMBER OF SHIPS OF PARTY OF (ACTING SHIP)
      IJ(IS)=IJ(IS)+1

CV SHIP WAS EMPTY: WRITE MESSAGE ABOUT NEW CREW
CV AND THAT SHIP IS BACK IN CONTROL
      WRITE(I8,52)(INAME(IQ0,IT),IQ0=1,3)
52    FORMAT(' OUR SHIP HAS BEEN REINFORCED BY THE ',3A4,' YOU ARE BACK
     * IN CONTROL')
      GO TO 1

CV =============================
CV TARGET M-O SHIP IS NOT EMPTY
CV =============================

CV SHIP WAS NOT EMPTY: WRITE MESSAGE ABOUT NEW CREW
11    WRITE(I8,12)(INAME(IQ0,IT),IQ0=1,3)
12    FORMAT(' OUR SHIP HAS BEEN REINFORCED BY THE ',3A4)

CV CALCULATE CREW NUMBERS ON TARGET & SOURCE,
CV SET BOARDING SUCCESSFUL FLAG, RETURN
      GO TO 1

CV IF CREW NUMBER TO BOARD > 0 (= BOARD TO):
CV CALCULATION OF NUMBER OF SURVIVORS (140)
100   IF(NUM.GT.0)GO TO 140
      WRITE(L,141)

CV ==================================
CV NONE TO BOARD OFF THE TARGET SHIP
CV ==================================

CV ELSE: (BOARD FROM)

141   FORMAT(' NONE OF OUR CREW IS ON THE TARGET SHIP -OPERATION CANCELL
     *ED')
      RETURN

CV ===================================
CV CALCULATION OF NUMBER OF SURVIVORS
CV ===================================

CV CALCULATE NUMBER OF SURVIVORS (I7) DEPENDING ON DAMAGE OF XXX
CV AND DISTANCE TO TARGET
140   I7=(120.-(MA(IT,JTK)+1)*DEFLT)*FLOAT(NUM)/100./DI(IENTC(IT),IENTR(
     *IT),ICOL,IROW)

CV IF DEFLECTOR STRENGTH OF TARGET <= 10 AND DAMAGE FACTOR
CV OF TRANSPORTER DECK <= 4 OR CALCULATED NUMBER OF SURVIVORS
CV (I7) > NUMBER OF CREW TO BOARD: NUMBER OF SURVIVORS = 95%
      IF((DEFLT.LE.10.0.AND.MA(IT,JTK).LE.4).OR.(I7.GT.NUM))I7=
     *(0.95*FLOAT(NUM))

CV CALCULATE CREW REMAINING ON SOURCE (IFROM)
      IFROM=IFROM-NUM

CV SET MINIMUM OF CALCULATED NUMBER OF SURVIVORS (I7) TO "0"
      IF(I7.LT.0)I7=0

CV WRITE NUMBER OF SURVIVORS SO FAR
      WRITE(L,101)I7
101   FORMAT(1X,I4, ' OF THE BOARDING PARTY HAVE SURVIVED SO FAR')

CV IF CALCULATED NUMBER OF SURVIVORS (I7) > 0:
CV DO TARGET TYPE SPECIFICS (102)
      IF(I7.GT.0)GO TO 102

CV =================================
CV BOARDING PARTY CRUSHING DECISION
CV =================================

CV IF DEFLECTOR STRENGTH OF TARGET<0: RETURN (?)
105   IF(DEFLT.LT.0)RETURN

CV WRITE MESSAGE "THE ENEMY HAS CRUSHED THE BOARDING PARTY"
      WRITE(L,103)
103   FORMAT(' THE ENEMY HAS CRUSHED THE BOARDING PARTY')
      RETURN

CV ======================
CV TARGET TYPE SPECIFICS
CV ======================

CV ================
CV EAGLE SPECIFICS
CV ================

CV IF BOARD TO <> 3=EAGLE: SKIP EAGLE SPECIFICS (104)
102   IF(J.NE.3)GO TO 104

CV IF NUMBER OF SURVIVORS (I7)<2: BOARDING PARTY CRUSHING DECISION (105)
      IF(I7.LT.2)GO TO 105

CV WRITE MESSAGE "THE BOARDING PARTY HAS SEIZED CONTROL OF EAGLE"
      WRITE(L,106)I
106   FORMAT(' THE BOARDING PARTY HAS SEIZED CONTROL OF EAGLE',I3)

CV GO TO 122 (?)
      GO TO 122

CV ================
CV ??? SPECIFICS
CV ================

CV IF BOARD TO M-O SHIP (2): (200)
104   IF(J.EQ.2)GO TO 200

CV IF TARGET ALREADY BOARDED: 130
      IF(ISID.NE.0)GO TO 130

CV CALCULATE RANDOM NUMBER OF SURVIVORS (IV) IN RANGE 1 TO 200
      CALL RANDO(IV,1,200)

CV IF NUMBER OF SURVIVORS > I7: BOARDING PARTY CRUSHING DECISION (105)
      IF(IV.GT.I7)GO TO 105

CV BOARDING PARTY SEIZED CONTROL (120)
      GO TO 120

CV
130   CALL RANDO(IV,1,100)

CV IF ...: BOARDING PARTY SEIZED CONTROL (120)
      IF(IV.LE.50*I7/IHORDE)GO TO 120

CV
      CALL RANDO(IV,0,I7/2)

CV
      IHORDE=IHORDE-IV

CV BOARDING PARTY CRUSHING DECISION (105)
      GO TO 105

CV ==============================
CV BOARDING PARTY SEIZED CONTROL
CV ==============================

120   WRITE(L,121)
121   FORMAT(' THE BOARDING PARTY HAS SEIZED CONTROL OF THE ENEMY SHIP')


CV =========================================
CV POSSIBLE SELF-DESTRUCT DEVICE ACTIVATION
CV =========================================

CV CALCULATE RANDOM SELF-DESTRUCT / MUTINY FACTOR IV IN RANGE 1 TO 7
122   CALL RANDO(IV,1,7)

CV IF RANDOM SELF-DESTRUCT FACTOR <> 1: NO SELF-DESTRUCTION (123)
      IF(IV.NE.1)GO TO 123

CV ELSE: SELF-DESTRUCTION
      WRITE(L,124)
124   FORMAT(' BUT AS THEIR LAST ACT, THE ENEMY HAS PLANTED A'/' SELF-DE
     *STRUCT DEVICE ON THEIR SHIP, WHICH HAS EXPLODED')
      DEFLT=-1.
      RETURN

CV ================
CV POSSIBLE MUTINY
CV ================

CV IF RANDOM  MUTINY FACTOR <> 2: NO MUTINY
123   IF(IV.NE.2)GO TO 125

CV ELSE: MUTINY
204   WRITE(L,126)
126   FORMAT(' BUT THE BOARDING PARTY HAS MUTINIED AND DEFECTED TO THE E
     *NEMY')
      IF(ISID.NE.0)IHORDE=IHORDE+I7
      RETURN

CV ====
CV ???
CV ====

125   IF(ISID.NE.0)GO TO 127
      I3=I3-1

CV VARIABLE ICNT NEVER USED AGAIN IN FUNCTION CHICO!!!
      ICNT=ICNT+1

CV
      GO TO 128

CV ====
CV ???
CV ====

127   IF(J.EQ.4.AND.IHORDE.EQ.0)GO TO 128

CV
      II(ISID)=II(ISID)-1

CV
      IF(J.NE.3)GO TO 128

CV DECREMENT NUMBER OF EAGLES UNDER CONTROL OF "ISID"
      NUME(ISID)=NUME(ISID)-1

CV INCREMENT NUMBER OF EAGLES UNDER CONTROL OF "IS"
      NUME(IS)=NUME(IS)+1

CV ====
CV ???
CV ====

128   ISID=IS
      II(ISID)=II(ISID)+1
      IHORDE=I7
      CHICO=.TRUE.
      RETURN

CV ====
CV ???
CV ====

200   I8=IBPSC(I)+4
      IF(ISURR(I).EQ.1)GO TO 235
      IF(ICHOE(I).EQ.2)GO TO 235
      N=(I+1)/2

CV SET "CREW REMAINING" FACTOR "A" TO 1.0
      A=1.0

CV CALCULATE RANDOM DECK INTRUSION INDEX IDEKU IN RANGE 1 TO 33
      CALL RANDO(IDEKU,1,33)
      IDEKL=IDEKU
      WRITE(I8,201)I7
201   FORMAT(1X,I4,' ENEMY TROOPS HAVE MATERIALIZED ONTO WHAT''S LEFT
     * OF ')

CV CALCULATE DECK DEPENDENT "CREW REMAINING" FACTOR "A"
CV FOR DECK INTRUSION INDEX IDEKU
      CALL TANRU(IDEKU)

CV CALCULATE RANDOM DEFECTION FACTOR (IV) IN RANGE 1 TO 6
      CALL RANDO(IV,1,6)

CV IF RANDOM DEFECTION FACTOR (IV) <> 5: NO DEFECTION
      IF(IV.NE.5)GO TO 202

CV ELSE: DEFECTION

      WRITE(I8,203)
203   FORMAT(' BUT THEY HAVE DEFECTED TO JOIN YOUR CREW')

CV ???
      GO TO 204

CV IF ...: BOARDING PARTY CRUSHING DECISION (105)
202   IF(.NOT.ZEPPO(IDEKU,IDEKL,DEFLT,ISID,IHORDE,IFROM))GO TO 105

CV
235   CHICO=.TRUE.
      WRITE(L,121)
      WRITE(I8,205)
205   FORMAT(' INTRUDERS HAVE TAKEN OVER OUR SHIP')
      IF(ICHOE(I).EQ.2)GO TO 206
      II(ISID)=II(ISID)-1
      IJ(ISID)=IJ(ISID)-1

CV ====
CV ???
CV ====

206   IBPSC(I)=IBPSC(IT)
      ICHOE(I)=1
      ISURR(I)=0
      IHORDE=I7
      II(IS)=II(IS)+1
      IJ(IS)=IJ(IS)+1
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C - ZEPPO -
C

CV ==========================
CV INPUT TRANSFER PARAMETERS
CV ==========================
CV L      - FORTRAN UNIT NUMBER OF M-O SHIP HAVING ITS TURN
CV I7     - NUMBER OF INTRUDERS
CV ===================
CV INTERNAL VARIABLES
CV ===================
CV KODOS  -
CV ISPRED - INTRUDERS SPREADING FACTOR?
CV
CV ==================
CV OUTPUT PARAMETERS
CV ==================
CV ZEPPO  - INTRUDERS SUCCESSFUL FLAG;
CV          FALSE = INTRUDERS FAILED / TRUE = INTRUDERS SUCCESSFUL
CV
CV INTRUDERS HANDLING
CV

      LOGICAL FUNCTION ZEPPO(IDEKU,IDEKL,DEFLT,ISID,IHORDE,IFROM)
      DIMENSION KODOS(7)
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /J/KODE(2,8),STATIC(4)
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /O/MA(4,33),K(4,14),NOMAP(4),MANUM(4),HIVEL(4,2),ITEMP(4),
     *NOMOV(4)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)

CV SET INTRUDERS SUCCESSFUL FLAG TO FALSE (FAILED)
      ZEPPO=.FALSE.

CV LOOP OVER KODOS COUNTER (IV) FOR 1 TO 7
      DO 21 IV=1,7

CV SET KODOS TO 0, NEXT KODOS
21    KODOS(IV)=0

CV SET NODEK TO 1
      NODEK=1

CF IF NUMBER OF INTRUDERS (I7) <= 15: VICTORY OVER INTRUDERS (36)
      IF(I7.LE.15)GO TO 36

CV WRITE INTRUDER DEFENSE OPTIONS
25    WRITE(I8,27)
27    FORMAT(' HERE IS A LIST OF YOUR INTRUDER CONTROL COMANDS-'/
     *' 0-PRINT THESE INSTRUCTIONS'/' 1-SOUND INTRUDER ALERT'/
     *' 2-EVACUATE PERSONNEL FROM ENEMY-HELD AREAS'/
     *' 3-CLOSE SECTION ISOLATION DOORS/SEAL OFF ENEMY-HELD AREAS'/
     *' 4-FLOOD ENEMY-HELD AREAS WITH NEURAL GAS'/
     *' 5-FLUSH RADIOACTIVE WASTE INTO ENEMY-HELD AREAS'/
     *' 6-DEPRESSURIZE ENEMY-HELD AREAS'/' 7-DISPATCH SECUTY TEAMS TO
     * ENEMY-HELD AREAS'/' 8-ACTIVATE SELF-DESTRUCT MECHANISM'/
     *' 9-OFFER NO RESISTANCE - SURRENDER')
      WRITE(L,28)
28    FORMAT(' STANDBY WHILE THE ENEMY FORCES TRY TO DEFEND THEIR SHIP')

CV ???
      KORAX=1

CV SET INTRUDERS SPREADING FACTOR TO 4
      ISPRED=4

      ADED=1.0
      BDED=0.95
      GO TO 32

CV CALCULATE CHANGED NUMBER OF INTRUDERS BY FACTOR BDED
34    I7=FLOAT(I7)*BDED

CV IF NUMBER OF INTRUDERS > IHORDE: 22
      IF(I7.GT.IHORDE)GO TO 22

CV IF NUMBER OF INTRUDERS <= 15: VICTORY OVER INTRUDERS (36)
      IF(I7.LE.15)GO TO 36

CV IF NUMBER OF INTRUDERS <= ??? (NODEK): 32
22    IF(I7.LE.NODEK)GO TO 32
      A=1.0
      WRITE(I8,29)
29    FORMAT(' INTRUDERS HAVE SPREAD INTO-')

CV LOOP OVER ??? (MIN) FOR 1 TO ISPRED*KORAX
      DO 30 MIN=1,ISPRED*KORAX

CV
      IF(MIN.GT.ISPRED/2)GO TO 31

CV
      IF(IDEKL.GE.33)GO TO 31

33    IDEKL=IDEKL+1
      NODEK=NODEK+1

      CALL TANRU(IDEKL)
      GO TO 30

31    IF(NODEK.GE.33)GO TO 30

      IF(IDEKU.LE.1)GO TO 33
      IDEKU=IDEKU-1

      CALL TANRU(IDEKU)
      NODEK=NODEK+1
30    CONTINUE
      KORAX=1

32    MIN=ISPRED*IHORDE/FLOAT(33-NODEK)+5

      CALL RANDO(IV,0,MIN)
      IV=IV*A*ADED
      IHORDE=IHORDE-IV
      IF(IHORDE.LE.0.OR.NODEK.GE.33)GO TO 35
39    WRITE(I8,37)IHORDE,I7
37    FORMAT(' CREW REMAINING=',I4,' INTRUDERS REMAINING=',I4/' ENTER Y
     *OUR INTRUDER CONTROL COMMAND:',$)
      READ(I8,38,ERR=410)IV
38    FORMAT(I1)
      IF(IV.LT.1)GO TO 41
      IF(IV.EQ.9)GO TO 35
      IF(IV.EQ.8)GO TO 49
      IF(IV.EQ.7)GO TO 48
      IF(KODOS(IV).EQ.0)GO TO 40
      WRITE(I8,401)
401   FORMAT(' YOU''VE ALREADY DONE THAT')
      GO TO 39
41    WRITE(I8,27)
      GO TO 39
410   IV=L
      L=I8

      CALL ILLDAT
      L=IV
      GO TO 39
40    KODOS(IV)=1
      GO TO (42,43,44,45,46,47),IV
42    WRITE(I8,50)
50    FORMAT(' INTRUDER ALERT SOUNDED')
      ADED=ADED*0.8
      GO TO 34
43    WRITE(I8,51)
51    FORMAT(' PERSONNEL EVACUATED')
      KORAX=2
      ADED=ADED*0.6
      GO TO 34

CV ============================
CV ENEMY-HELD AREAS SEALED OFF
CV ============================

44    WRITE(I8,52)
52    FORMAT(' ENEMY-HELD AREAS SEALED OFF -  ENEMY NOW RESORTING TO BLAS
     *TING THROUGH BULKHEADS')

CV HALVE SPREADING FACTOR (ISPRED)
      ISPRED=ISPRED/2
      GO TO 34

CV ========================
CV ENEMY-HELD AREAS GASSED
CV ========================

45    WRITE(I8,53)
53    FORMAT(' ENEMY-HELD AREAS GASSED')

      CALL RANDO(MIN,85,95)
      I7=I7*(FLOAT(MIN)/100.)
      GO TO 34

CV ================================================
CV ENEMY-HELD AREAS FLOODED WITH RADIOACTIVE WASTE
CV ================================================

46    WRITE(I8,54)
54    FORMAT(' ENEMY AREAS FLOODED WITH RADIOACTIVE WASTE')

      CALL RANDO(MIN,77,85)
      ADED=ADED+FLOAT(MIN)/100.
      BDED=BDED*FLOAT(MIN)/100.
      GO TO 34

CV ===============================
CV ENEMY-HELD AREAS DEPRESSURIZED
CV ===============================

47    WRITE(I8,55)
55    FORMAT(' ENEMY AREAS DEPRESSURIZED')
      ADED=ADED*1.3
      BDED=BDED*0.7
      GO TO 34

CV
480   IV=L
      L=I8

      CALL ILLDAT
      L=IV
48    WRITE(I8,56)
56    FORMAT(' ENTER NUMBER OF CREW TO MAKE UP THE SECURITY FORCES')
      READ(I8,57,ERR=480)MIN
57    FORMAT(I)

CV
      IF(MIN.LT.0.OR.MIN.GT.IHORDE)GO TO 48

CV
      CALL RANDO(IV,1,100)

CV IF ...: VICTORY OVER INTRUDERS (36)
      IF(IV.LE.50*MIN/I7)GO TO 36

CV ELSE: DEFEATED BY INTRUDERS

      WRITE(I8,59)
59    FORMAT(' ENEMY FORCES HAVE WIPED OUT OUR SECURITY FORCES')

      IHORDE=IHORDE-MIN

      CALL RANDO(IV,0,IFIX(MIN*0.75))

      I7=I7-IV
      GO TO 34

49    IF(KODOS(7).EQ.0)GO TO 58
      WRITE(I8,60)
60    FORMAT(' THE SELF-DESTRUCT ROUTINE DOES NOT WORK')
      GO TO 39

58    IF(IDEKU.GT.ISID)GO TO 61

      WRITE(I8,62)
62    FORMAT(' SINCE THE BRIDGE HAS BEEN CAPTURED,')
      WRITE(I8,60)
      GO TO 39

61    CALL RANDO(IV,1,3)
      KODOS(7)=1

      IF(IV.GT.2)GO TO 63
      WRITE(I8,60)
      GO TO 34

63    DO 75 IV=1,4
      IF(ICHOE(IV).EQ.0)GO TO 75
      I8=IV+4
      WRITE(I8,64)(INAME(IQ0,IT),IQ0=1,3)
64    FORMAT(1X,3A4,' SELF-DESTRUCT ROUTINE ACTIVATED')

75    CONTINUE

      DEFLT=-1.
      RETURN

CV =======================
CV VICTORY OVER INTRUDERS
CV =======================

36    WRITE(I8,65)
65    FORMAT(' OUR FORCES HAVE VANQUISHED THE INTRUDERS')

CV CALCULATE RANDOM BOMB DETONATING FACTOR IV IN RANGE 1 TO 5
      CALL RANDO(IV,1,5)

CV IF RANDOM BOMB DETONATING FACTOR IV > 1: NO BOMB DETONATING
      IF(IV.GT.1)GO TO 69

CV ELSE: BOMB DETONATING

      WRITE(I8,66)
66    FORMAT(' BUT THE LAST INTRUDER DETONATED A BOMB BEFORE HIS CAPTURE
     * DAMAGE REPORT-')

CV LOOP OVER M-O SHIPS IV FOR 1 TO 4
      DO 67 IV=1,4

CV IF LOOPED M-O SHIP NOT IN GAME: NEXT M-O SHIP
      IF(ICHOE(IV).EQ.0)GO TO 67

CV IF LOOPED M-O SHIP = INTRUDED SHIP (I): NEXT M-O SHIP
      IF(IV.EQ.I)GO TO 67

CV WRITE BOMB MESSAGE
      MIN=IV+4

      WRITE(MIN,68)(INAME(IQ0,I),IQ0=1,3)
68    FORMAT(' A BOMB HAS BEEN DETONATED IN THE ',3A4,'-DAMAGE REPORT-')

CV NEXT M-O SHIP
67    CONTINUE

CV SET DAMAGE FACTOR IVV TO 8
      IVV=8

CV SET DEFLECTOR STRENGTH FOR DAMAGE CALCULATION
CV TO DEFLECTOR STRENGTH OF INTRUDED SHIP (I)
      DFLCT(I)=DEFLT

      NDEAD(I)=IHORDE

CV CALCULATE AND WRITE DAMAGES TO M-O SHIP I BY DAMAGE FACTOR IVV
      CALL GRUP1(IVV,I)

      DEFLT=DFLCT(I)
      IHORDE=NDEAD(I)

69    IF(I.GT.2)GO TO 70
      WRITE(I8,71)
71    FORMAT(' USING TRUTH DRUGS, PSYCHOTRICORDERS, AND VERIFIER
     * SCANS,')
      GO TO 72

70    WRITE(I8,73)
73    FORMAT(' USING MIND-SIFTERS AND AGONIZERS,')
72    WRITE(I8,74)(INAME(IQ0,IT),IQ0=1,3),IFROM,DFLCT(IT),PHASR(IT),
     *TWARP(IT),IPHOT(IT),ITEMP(IT)
74    FORMAT(' WE HAVE UNCOVERED THE FOLLOWING INFORMATION FROM THE PRIS
     *ONERS-'/' THEY ARE FROM THE ',3A4,' THEIR SHIP''S CREW IS NOW',I4,
     *'DEFLECTORS= ',F11.7,' UNITS. PHASERS=',F13.7,' UNITS'/' MAX. WARP
     *=',F10.7,'. NO. TORPEDOES/DISRUPTORS=',I3,' ENGINE TEMP=',I5,
     *' DEGREES')

      CALL RANDO(IV,1,3)

      IF(IV.GT.1)RETURN

      CALL RANDO(IV,1,8)

      KODE(IS,IV)=1

      RETURN

35    ZEPPO=.TRUE.
      RETURN
      END


C DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C TANRU   -
C

CV CALCULATE DECK DEPENDENT "CREW REMAINING" FACTOR "A"

      SUBROUTINE TANRU(NOMAD)
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      CALL FORBIN(N,NOMAD,I8,' ')

CV IF SHIP = HAVOC OR CARNAGE: SKIP TO 30
      IF(I.GT.2)GO TO 30
      IF(NOMAD.EQ. 4)A=A+0.2
      IF(NOMAD.EQ. 5)A=A+0.3
      IF(NOMAD.EQ. 6)A=A+2.2
      IF(NOMAD.EQ. 7)A=A+1.2
      IF(NOMAD.EQ.21)A=A+0.3
      IF(NOMAD.EQ.22)A=A+0.3
      IF(NOMAD.EQ.23)A=A+0.2
      RETURN
30    IF(NOMAD.EQ. 8)A=A+1.8
      IF(NOMAD.EQ.10)A=A+0.1
      IF(NOMAD.EQ.11)A=A+0.2
      IF(NOMAD.EQ.17)A=A+0.4
      IF(NOMAD.EQ.19)A=A+1.1
      IF(NOMAD.EQ.21)A=A+0.4
      IF(NOMAD.EQ.22)A=A+0.9
      RETURN
      END


C DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C LARRY -
C

CV TRACTOR BEAM HANDLING

      LOGICAL FUNCTION LARRY(J)
      LOGICAL NOTRAC
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /P/IPULL(4),IPUSH(4),PULL(4),PUSH(4),IPULLR(4),IPULLC(4),
     *IPUSHR(4),IPUSHC(4)

CV SET TARGET IN TRACTOR RANGE FLAG (LARRY) TO TRUE
      LARRY=.TRUE.

CV IF TRACTOR BEAM DEFECTIVE:
CV SET TARGET IN TRACTOR RANGE FLAG (LARRY) TO FALSE, RETURN, END (205)
      IF(NOTRAC(IT))GO TO 205

CV SET YANK FLAG TO UNLOCKED
200   IPULL(IT)=0

CV IF DISTANCE BETWEEN PULLING AND PULLED VESSEL <= TRACTOR RANGE (DISTP): 202
      IF(DI(IENTC(IT),IENTR(IT),IPULLC(IT),IPULLR(IT)).LE.DISTP)GOTO 202

CV WRITE MESSAGE "TARGET OUT OF TRACTOR RANGE"
      WRITE(L,201)
201   FORMAT(' TARGET OUT OF TRACTOR RANGE')

CV SET TARGET IN TRACTOR RANGE FLAG (LARRY) TO FALSE, RETURN, END (205)
      GO TO 205

CV IF PULL ENERGY > AVAILABLE ENERGY:
CV REDUCE PULL ENERGY TO AVAILABLE ENERGY
202   IF(PULL(IT).GT.TWARP(IT))PULL(IT)=TWARP(IT)

CV REDUCE AVAILABLE ENERGY BY PULL ENERGY
      TWARP(IT)=TWARP(IT)-PULL(IT)

CV IF MOVEBILITY OF SHIP = FULL: SET MOVEBILITY TO REDUCED
      IF(IGO(IT).EQ.2)IGO(IT)=1

CV WRITE MESSAGE "TRACTOR BEAM ON"
      WRITE(L,203)
203   FORMAT(' TRACTOR BEAM ON')

CV GET WHAT IS AT PULLING POSITION INTO NA
      NA=MAP(IPULLR(IT),IPULLC(IT))

CV OBJECT AT PULLING POSITION IS AN EAGLE: 2040
      IF(NA.EQ.IGLE)GO TO 2040

CV OBJECT AT PULLING POSITION IS EMPTY, ION STORM OR STAR:
CV SET TARGET IN TRACTOR RANGE FLAG (LARRY) TO FALSE, RETURN, END
      IF(NA.EQ.IBLK.OR.NA.EQ.III.OR.NA.EQ.ISTAR)GO TO 205

CV OBJECT AT PULLING POSITION IS THE PULLER ITSELF:
CV SET TARGET IN TRACTOR RANGE FLAG (LARRY) TO FALSE, RETURN, END (205)
      IF(NA.EQ.IEE(IT))GO TO 205

CV LOOP OVER M-O SHIPS IV FOR 1 TO 4
      DO 269 IV=1,4

CV IF OBJECT AT PULLING POSITION IS A MINE OF PULLER: 2040
      IF(NA.EQ.IM(IV))GO TO 2040

CV NEXT M-O SHIP
269   CONTINUE

CV SET YANK FLAG TO LOCKED
      IPULL(IT)=1

CV SET TARGET IN TRACTOR RANGE FLAG (LARRY) TO FALSE, RETURN, END (205)
      GO TO 205

CV SET RAD TO 10 * PULL ENERGY
2040  RAD=PULL(IT)*10.

CV CALCULATE PULL ANGLE (IN DEGREES)
      BERNG=ANG(IENTR(IT)-IPULLR(IT),IENTC(IT)-IPULLC(IT))*
     *3.14159265/180.

CV
      CALL MOE(IPULLC(IT),IPULLR(IT),RAD,BERNG)

CV
      IF(DISTP/10.0.LT.PULL(IT))TWARP(IT)=TWARP(IT)+PULL(IT)-DISTP/10.
      RETURN

205   LARRY=.FALSE.
      RETURN
      END



C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K 7 -
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C - CURLY -
C

CV DEFLECTOR BEAM HANDLING

      LOGICAL FUNCTION CURLY(J)
      LOGICAL NODEFL
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /P/IPULL(4),IPUSH(4),PULL(4),PUSH(4),IPULLR(4),IPULLC(4),
     *IPUSHR(4),IPUSHC(4)

CV
      CURLY=.TRUE.

CV
      IF(NODEFL(IT))GO TO 305

CV
300   IPUSH(IT)=0

CV
      AJUST=DI(IENTC(IT),IENTR(IT),IPUSHC(IT),IPUSHR(IT))

CV
      IF(AJUST.LE.DISTP)GO TO 302

      WRITE(L,301)
301   FORMAT(' TARGET OUT OF DEFLECTOR RANGE')
      GO TO 305

CV
302   IF(PUSH(IT).GT.DFLCT(IT))PUSH(IT)=DFLCT(IT)

CV
      DFLCT(IT)=DFLCT(IT)-PUSH(IT)

CV IF MOVEBILITY OF SHIP = FULL: SET MOVEBILITY TO REDUCED
      IF(IGO(IT).EQ.2)IGO(IT)=1

      WRITE(L,303)
303   FORMAT(' DEFLECTOR BEAM ON')
      NA=MAP(IPUSHR(IT),IPUSHC(IT))
      IF(NA.EQ.IGLE)GO TO 304

      IF(NA.EQ.IBLK.OR.NA.EQ.III.OR.NA.EQ.ISTAR)GO TO 305

      IF(NA.EQ.IEE(IT))GO TO 305

      DO 284 IV=1,4
      IF(NA.EQ.IM(IV))GO TO 304

284   CONTINUE
      IPUSH(IT)=1
      GO TO 305

304   RAD=PUSH(IT)
      IF(AJUST+RAD.GT.DISTP)RAD=DISTP-AJUST
      BERNG=ANG(IPUSHR(IT)-IENTR(IT),IPUSHC(IT)-IENTC(IT))*3.141592615/
     *180.

      CALL MOE(IPUSHC(IT),IPUSHR(IT),RAD,BERNG)

      IF(DISTP.LT.PUSH(IT))DFLCT(IT)=DFLCT(IT)+PUSH(IT)-DISTP
      RETURN

305   CURLY=.FALSE.
      RETURN
      END


C -- DONALD ECCLESTONE SUBPROGRAM FOR T R E K. 7 --
C CONVERTED TO PC BY: DAN GAHLINGER
C TYPE-EXACT CHECK 04/27/2000 BY: D.G.
C
C - MOE -
C

CV PUSH/PULL ROUTINE / EAGLE MOVE/MINE HIT/COLLISION DETECTION

      SUBROUTINE MOE(IPUC,IPUR,RAD,BERNG)
      COMMON /A/IT,IS,II(2),IJ(2),I3,JS,ISHAK,NOSTOP
      COMMON /C/L,A,B,I,NA,IV,I7,I8,N,DISTP,AJUST,MIN,ISTAT,JTK,KOENIG,
     *IGNORE,IO,IGOL(80)
      COMMON /D/DFLCT(4),DFLCK(8),DFLCB(2)
      COMMON /E/PHASR(4),TWARP(4),IPHOT(4),NDEAD(4),ISURR(4)
      COMMON /L/IENTR(4),IENTC(4),IKLNR(8),IKLNC(8),IGLER(25),IGLEC(25),
     *IBASR(2),IBASC(2),LI2(4),LI2R(5),LI2C(5),IGO(4),MINES
      COMMON /M/MAP(60,60),IBLK,IENM1(8),IEE(4),IGLE,IM(4),III,ISTAR
      COMMON /N/INAME(3,4),IENM2(4,8),ISIDE(3,2),IBASE(2)
      COMMON /O/MA(4,33),K(4,14),NOMAP(4),MANUM(4),HIVEL(4,2),ITEMP(4),
     *NOMOV(4)
      COMMON /P/IPULL(4),IPUSH(4),PULL(4),PUSH(4),IPULLR(4),IPULLC(4),
     *IPUSHR(4),IPUSHC(4)
      COMMON /T/ICHOE(4),ICHOS(8),ICHOB(2)
      COMMON /U/LAUNCH,NUMOUT,NUME(2)

CV CALCULATE SPECIAL DAMAGE (FROM EAGLE) NUMBER FROM X (?)
CV AND DAMAGE FACTOR IV
      KLIGAT(X)=IFIX(ALOG((101.-X)*FLOAT(IV))/0.700619195-1.8185)

CV ???
204   N=4

CV CALCULATE RANDOM DAMAGE FACTOR IV IN RANGE 20 TO 100
      CALL RANDO(IV,20,100)

CV SET SPECIAL DAMAGE FACTOR = 1
      IIV=1

CV IF EAGLE IN TARGET POSITION (IPUR/IPUC): SPECIAL DAMAGE FACTOR = 2
      IF(MAP(IPUR,IPUC).EQ.IGLE)IIV=2

CV SET IGNORE ION STORM FLAG
      IGNORE=1

CV IF NO EAGLE IN TARGET POSITION: 206
      IF(IIV.EQ.1)GO TO 206

CV ELSE: EAGLE IN TARGET POSITION

CV RESET IGNORE ION STORM FLAG
      IGNORE=0

CV LOOP OVER EAGLES IVV FOR 1 TO LAUNCH
      DO 209 IVV=1,LAUNCH

CV IF EAGLE AT PUSH/PULL POSITION: 206
      IF(IPUC.EQ.IGLEC(IVV).AND.IPUR.EQ.IGLER(IVV))GO TO 206

CV NEXT EAGLE
209   CONTINUE

      ITZRO=0

CV
206   CALL HORTA(IPUR,IPUC,ITZRO,ITZRO,RAD,BERNG,ITZRO,
     *ITZRO,IGNORE,ITZRO,ITZRO)

      IF(MIN.GT.4)GO TO 308

      DO 270 I71=1,4

      IF(ICHOE(I71).EQ.0)GO TO 270

      I81=I71+4

CV IF EAGLE: 271
      IF(IIV.EQ.2)GO TO 271

CV ===============
CV MINE RECOVERED
CV ===============
CV IF ACTING M-O SHIP AT I7/I8: MINE RECOVERED (221)
      IF(MAP(I7,I8).EQ.IEE(IT))GO TO 221

CV ================
CV MINE HIT VESSEL
CV ================
      WRITE(I81,272)(INAME(IQ0,MIN),IQ0=1,3)
272   FORMAT(' MINE HIT ',3A4,'-DAMAGE REPORT-')
      GO TO 270

CV ===================
CV EAGLE HIT M-O SHIP
CV ===================
271   WRITE(I81,273)IVV,(INAME(IQ0,MIN),IQ0=1,3)
273   FORMAT(' EAGLE',I3,' HIT THE ',3A4,'-DAMAGE REPORT-')
270   CONTINUE

      CALL GRUP1(KLIGAT(DFLCT(MIN)),MIN)

      GO TO 215

CV ==================
CV MINE HIT C-O SHIP
CV ==================
308   MIN=MIN-4

CV NEITHER M-O NOR C-O SHIP: 205
      IF(MIN.GT.8)GO TO 205

      DO 274 I71=1,4
      IF(ICHOE(I71).EQ.0)GO TO 274
      I81=I71+4
      IF(IIV.EQ.2)GO TO 210
      WRITE(I81,207)(IENM2(I,MIN),I=1,4)
207   FORMAT(' MINE HIT ',4A4,'-DAMAGE REPORT-')
      GO TO 274

CV ===================
CV EAGLE HIT C-O SHIP
CV ===================
210   WRITE(I81,211)IVV,(IENM2(I,MIN),I=1,4)
211   FORMAT(' EAGLE',I3,' HIT ',4A4,' -DAMAGE REPORT-')
274   CONTINUE

      CALL GRUP3(KLIGAT(DFLCK(MIN)),MIN)
      GO TO 215

CV MIN = 13 (STAR)

CV IF MIN > 13: 224
205   IF(MIN.GT.9)GO TO 224

CV ELSE: STAR/STARBASE

CV LOOP OVER I71 FOR 1 TO 2
      DO 275 I71=1,2

CV IF STARBASE ROW POSITION OF I71 <> I7: I71 IS NOT A STARBASE, NEXT I71
      IF(IBASR(I71).NE.I7)GO TO 275

CV IF STARBASE COL POSITION OF I71 = I8: I71 IS A STARBASE, 276
      IF(IBASC(I71).EQ.I8)GO TO 276

CV NEXT I71
275   CONTINUE

CV GO TO "MINE HAS COLLIDED WITH A STAR"
      GO TO 220

CV ==================
CV MINE HIT STARBASE
CV ==================
276   MIN=I71
      DO 277 I71=1,4
      IF(ICHOE(I71).EQ.0)GO TO 277
      I81=I71+4
      IF(IIV.EQ.2)GO TO 278
      WRITE(I81,279)(ISIDE(IQ0,MIN),IQ0=1,3),IBASE(MIN)
279   FORMAT(' MINE HIT ',3A4,' STARBASE',I3,'-DAMAGE REPORT-')
      GO TO 277

CV ===================
CV EAGLE HIT STARBASE
CV ===================
278   WRITE(I81,280)IVV,(ISIDE(IQ0,MIN),IQ0=1,3),IBASE(MIN)
280   FORMAT(' EAGLE',I3,' HIT ',3A4,' STARBASE',I3,'-DAMAGE REPORT-')
277   CONTINUE

      CALL GRUP2(KLIGAT(DFLCB(MIN)/3.),MIN)
      GO TO 215

220   IF(IIV.EQ.2)GO TO 227

CV ==============================
CV MINE HAS COLLIDED WITH A STAR
CV ==============================
      WRITE(L,228)
228   FORMAT(' MINE HAS COLLIDED WITH A STAR')
      GO TO 215

227   DO 282 I71=1,4
      IF(ICHOE(I71).EQ.0)GO TO 282
      I81=I71+4

CV ===============================
CV EAGLE HAS COLLIDED WITH A STAR
CV ===============================
      WRITE(I81,229)IVV
229   FORMAT(' EAGLE',I3,' HAS COLLIDED WITH A STAR')
282   CONTINUE
      GO TO 215

CV ===============
CV MINE RECOVERED
CV ===============
221   WRITE(L,231)
231   FORMAT(' OUR SHIP HAS RECOVERED A MINE')
      IPHOT(IT)=IPHOT(IT)+1
      GO TO 215

CV =============================
CV EAGLE DESTROYED IN ION STORM
CV =============================
222   IF(MIN.GT.15)GO TO 225
      DO 283 I71=1,4
      IF(ICHOE(I71).EQ.0)GO TO 283
      I81=I71+4
      WRITE(I81,233)IVV
233   FORMAT(' EAGLE',I3,' DESTROYED IN ION STORM')
283   CONTINUE
      GO TO 215

CV ==============================================
CV MINE DESTROYED ON COLLISION WITH ANOTHER MINE
CV ==============================================
223   IF(MIN.GT.14)GO TO 222
      MAP(I7,I8)=IBLK
      IF(IIV.EQ.2)GO TO 234
      WRITE(L,235)
235   FORMAT(' MINE DESTROYED ON COLLISION WITH ANOTHER MINE')
      GO TO 215

CV ===================================
CV EAGLE DESTROYED WHEN HIT WITH MINE
CV ===================================
234   DO 281 I71=1,4
      IF(ICHOE(I71).EQ.0)GO TO 281
      I81=I71+4
      WRITE(I81,236)IVV
236   FORMAT(' EAGLE',I3,' DESTROYED WHEN HIT WITH MINE')
281   CONTINUE
      GO TO 215

224   IF(MIN.GT.10)GO TO 223
      DO 237 I=1,LAUNCH
      IF(I7.EQ.IGLER(I).AND.I8.EQ.IGLEC(I))GO TO 238
237   CONTINUE

CV =================================================
CV EAGLE COLLIDED WITH EAGLE, BOTH EAGLES DESTROYED
CV =================================================
238   DO 242 I71=1,4
      IF(ICHOE(I71).EQ.0)GO TO 242
      I81=I71+4
      IF(IIV.EQ.1)GO TO 240
      WRITE(I81,239)IVV,I
239   FORMAT(' EAGLE',I3,' COLLIDED WITH EAGLE',I3/' BOTH EAGLES
     * DESTROYED')
      GO TO 242

CV ===================================
CV EAGLE DESTROYED WHEN IT HIT A MINE
CV ===================================
240   WRITE(I81,241)I
241   FORMAT(' EAGLE',I3,' DESTROYED WHEN IT HIT A MINE')
242   CONTINUE

      CALL BOOM(I)
      GO TO 215
225   IF(MIN.GT.16)GO TO 226
      N=2
243   I71=I7
      I81=I8
      ITZRO=0
      ITVL1=1
      RTZRO=0.0
      RTVL1=1.5

      CALL HORTA(I71,I81,IENTR(IT),IENTC(IT),RTVL1,
     *ITZRO,ITZRO,ITZRO,ITVL1,ITZRO,ITZRO)

      IF(MIN.EQ.20)GO TO 243
226   MAP(IPUR,IPUC)=IBLK
      IF(IIV.EQ.2)GO TO 244

CV ===========
CV MINE MOVED
CV ===========
      MAP(I7,I8)=IM(IT)
      WRITE(L,245)I8,I7
245   FORMAT(' MINE MOVED TO (',I2,',',I2,')')
      GO TO 400

CV ===============
CV MOVE EAGLE IVV
CV ===============
244   MAP(I7,I8)=IGLE
      IGLER(IVV)=I7
      IGLEC(IVV)=I8
      WRITE(L,246)IVV,I8,17
246   FORMAT(' EAGLE',I3,' MOVED TO (',I2,',',I2,')')
      GO TO 400

CV ==================
CV MANAGE CASUALTIES
CV ==================

CV IF EAGLE: DESTROY EAGLE (247)
215   IF(IIV.EQ.2)GO TO 247

CV ELSE: DELETE SYMBOL IN MAP AT IPUR/IPUC
      MAP(IPUR,IPUC)=IBLK

CV RETURN
      GO TO 400

CV DESTROY EAGLE IVV
247   CALL BOOM(IVV)

CV RETURN
      GO TO 400

400   RETURN
      END
